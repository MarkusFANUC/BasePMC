//:--------------------------------------------------------------------------:
//: File              : axis01__PATH__.src                                       :
//: Creation date     : 16.09.2013                                           :
//: Last modification : see table below                                      :
//: Author            : FANUC FA Switzerland / Peter Bloesch                 :
//: Description       : path specific management of 1st axis                 :
//:--------------------------------------------------------------------------:
//
//:--------------------------------------------------------------------------:
//:                     Modification                                         :
//:--------------------------------------------------------------------------:
//: Date   :Version: Sign  :Description                                      :
//:--------------------------------------------------------------------------:
//:05.11.18: 2.1.0 :Blp    :In case of brake control, the activation of the  :
//:        :       :       :*IT signal is accelerated when wp coordinate     :
//:        :       :       :preset is executed or hold to run button is      :
//:        :       :       :pressed.                                         :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:

// ===========================================================================
// Code
// ===========================================================================

// ---------------------------------------------------------------------------
// _PATH__: overtravel check
// if there are limit switches for the 1st axis,
// replace LOG1 with the real input signals Xxxx.y
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: overtravel check
if there are limit switches for the 1st axis,
replace LOG1 with the real input signals Xxxx.y
*)
#endif
                 RD        LOG1                 ;"logic one"
                 WRT       *+L1__PATH__         ;"overtravel + 1st axis"

                 RD        LOG1                 ;"logic one"
                 WRT       *-L1__PATH__         ;"overtravel - 1st axis"

// ---------------------------------------------------------------------------
// reference position return
// ---------------------------------------------------------------------------
#if ref_dir_1st_axis__PATH__ > 0
   #include "axis01ref__PATH__.src"    // reference position return 1st axis
#endif

// ---------------------------------------------------------------------------
// _PATH__: led 1st axis selected
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: led 1st axis selected
*)
#endif
                 RD        M_K1AX__PATH__       ;"memory 1st axis selected"
#if ref_dir_1st_axis__PATH__ > -1
                 AND.NOT   REFRUN__PATH__       ;"reference return active"
   #if ref_dir_1st_axis__PATH__ > 0
                 OR        R_REF1__PATH__       ;"reference return 1st axis"
   #endif
#endif
                 AND       M_MAN__PATH__        ;"manual mode active"
                 WRT       L_1AX__PATH__        ;"led 1st axis sel."

// ---------------------------------------------------------------------------
// _PATH__: jog feed
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: jog feed
*)
#endif
                 RD        M_K1AX__PATH__       ;"memory 1st axis selected"
                 AND       M_KPLUS__PATH__      ;"memory axis plus dir."
                 AND.NOT   M_REF__PATH__        ;"mode ref selected"
#if ref_dir_1st_axis__PATH__ > 0
                 OR        aref_1p__PATH__      ;"ref: move 1st axis +dir."
#endif
                 AND       AX_MEN__PATH__       ;"axis move enable"
#if spindle_support__PATH__ == 4 && spindle_sv_axis__PATH__ == 1 && spindle_support_P01 > -1
                 AND.NOT   EAX1__PATH__         ;"1st axis is pmc axis"
#endif
                 WRT       J1+__PATH__          ;"feed 1st axis + direction"
                 WRT       L_1AXP__PATH__       ;"led 1st axis + dir."

                 RD        M_K1AX__PATH__       ;"memory 1st axis selected"
                 AND       M_KMINUS__PATH__     ;"memory axis minus dir."
                 AND.NOT   M_REF__PATH__        ;"mode ref selected"
#if ref_dir_1st_axis__PATH__ > 0
                 OR        aref_1m__PATH__      ;"ref: move 1st axis -dir."
#endif
                 AND       AX_MEN__PATH__       ;"axis move enable"
#if spindle_support__PATH__ == 4 && spindle_sv_axis__PATH__ == 1 && spindle_support_P01 > -1
                 AND.NOT   EAX1__PATH__         ;"1st axis is pmc axis"
#endif
                 WRT       J1-__PATH__          ;"feed 1st axis -direction"
                 WRT       L_1AXM__PATH__       ;"led 1st axis - dir."

// ---------------------------------------------------------------------------
// _PATH__: workpiece coordinate system preset
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: workpiece coordinate system preset
*)
#endif
                 RD        REQWP1__PATH__       ;"req wp coord preset 1st axis"
                 AND       WPRST1__PATH__       ;"wp coord. preset 1st axis"
                 AND       WPSF1__PATH__        ;"wp coord. preset fin 1st axis"
                 OR        RST__PATH__          ;"resetting signal"
                 RST       REQWP1__PATH__       ;"req wp coord preset 1st axis"

                 RD        M101__PATH__         ;"wp coord preset 1st axis"
                 AND       WPRST1__PATH__       ;"wp coord preset 1st axis"
                 AND       WPSF1__PATH__        ;"wp coord preset fin 1st ax"
                 RST       M101__PATH__         ;"wp coord preset 1st axis"

                 RD        FE_RST__PATH__       ;"falling edge reset"
                 AND       wppsreq1__PATH__     ;"req wp coord. preset 1st ax"
                 RD.STK    FEPGMT__PATH__       ;"falling edge program test"
                 AND.NOT   PGMRUN__PATH__       ;"program running"
                 OR.STK    
                 OR        REQWP1__PATH__       ;"req wp coord preset 1st axis"
                 OR        M101__PATH__         ;"wp coord preset 1st axis"
                 RD.STK    WPRST1__PATH__       ;"wp coord. preset 1st axis"
                 AND.NOT   WPSF1__PATH__        ;"wp coord. preset fin 1st axis"
                 OR.STK    
                 AND.NOT   RST__PATH__          ;"resetting signal"
                 WRT       WPRST1__PATH__       ;"wp coord. preset 1st axis"

                 RD        FEPGMT__PATH__       ;"falling edge program test"
                 AND       PGMRUN__PATH__       ;"program running"
                 RD.STK    wppsreq1__PATH__     ;"req wp coord. preset 1st ax"
                 AND.NOT   FE_RST__PATH__       ;"falling edge reset"
                 OR.STK    
                 WRT       wppsreq1__PATH__     ;"req wp coord. preset 1st ax"

#if brake_1st_axis__PATH__ == 0
// ---------------------------------------------------------------------------
// _PATH__: axis interlock
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: axis interlock
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       POW_OK__PATH__       ;"power supply axis ok"
   #else
                 RD        POW_OK__PATH__       ;"power suply axis ok"
   #endif
                 AND.NOT   WPRST1__PATH__       ;"wp coord. preset 1st axis"
                 WRT       *IT1__PATH__         ;"interlock 1st axis"

// ---------------------------------------------------------------------------
// _PATH__: servo off
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: servo off
*)
   #endif
                 RD.NOT    POW_OK__PATH__       ;"power suply axis ok"
                 WRT       SVF1__PATH__         ;"servo off 1st axis"
#else
// ---------------------------------------------------------------------------
// _PATH__: brake control
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: brake control
*)
   #endif
                 RD        POW_OK__PATH__       ;"power supply axis ok"
                 AND       SA1__PATH__          ;"1st axis servo ready"
                 WRT       relb_1st__PATH__     ;"release brake 1st axis"

// ---------------------------------------------------------------------------
// _PATH__: delay brake released
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: delay brake released
*)
   #endif
                 RD        relb_1st__PATH__     ;"release brake 1st axis"
                 TMRB      
                           0
                           96
                 WRT       brel_1st__PATH__     ;"brake 1st axis released"

   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       brel_1st__PATH__     ;"brake 1st axis released"
   #else
                 RD        brel_1st__PATH__     ;"brake 1st axis released"
   #endif
                 AND.NOT   WPRST1__PATH__       ;"wp coord preset 1st axis"
                 WRT       *IT1__PATH__         ;"interlock 1st axis"

// ---------------------------------------------------------------------------
// _PATH__: delay brake clamped
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: delay brake clamped
*)
   #endif
                 RD.NOT    relb_1st__PATH__     ;"release brake 1st axis"
                 TMRB      
                           0
                           48
                 WRT       1st_soff__PATH__     ;"1st axis servo off"

// ---------------------------------------------------------------------------
// _PATH__: servo off
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: servo off
*)
   #endif
                 RD.NOT    FICYCL               ;"first cycle finished"
                 OR        1st_svf1__PATH__     ;"1st servo off 1st axis"
                 AND.NOT   POW_OK__PATH__       ;"power supply axis ok"
                 WRT       1st_svf1__PATH__     ;"1st servo off 1st axis"

                 RD        1st_svf1__PATH__     ;"1st servo off 1st axis"
                 OR        1st_soff__PATH__     ;"1st axis servo off"
                 WRT       SVF1__PATH__         ;"servo off 1st axis"
#endif
// ---------------------------------------------------------------------------
// _PATH__: external deceleration
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: external deceleration
*)
#endif
                 RD.NOT    SETUP__PATH__        ;"set-up active"
                 WRT       *+ED1__PATH__        ;"external dec. + 1st axis"
                 WRT       *-ED1__PATH__        ;"external dec. - 1st axis"

#if dcs_support == 1
// ---------------------------------------------------------------------------
// _PATH__: DCS MCC off signal
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS MCC off signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        *MCFV1__PATH__       ;"MCC off signal 1st axis"
   #endif
                 WRT       M*MCFV1__PATH__      ;"1st axis MCC off signal"

// ---------------------------------------------------------------------------
// _PATH__: DCS monitoring result signal
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS monitoring result signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        RSV1__PATH__         ;"monitoring result 1st axis"
   #endif
                 WRT       MRSV1__PATH__        ;"1st axis monit. result"

// ---------------------------------------------------------------------------
// _PATH__: position information effect signal
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
DCS position information effect signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        POSE1__PATH__        ;"pos. inf. effect 1st axis"
   #endif
                 WRT       MPOSE1__PATH__       ;"1st axis pos. inf. effect"

// ---------------------------------------------------------------------------
// _PATH__: DCS safety check activation
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS safety check activation
*)
   #endif
                 RD        *SGOPN               ;"guard state doors closed"
                 WRT       *VLDV1__PATH__       ;"safety check req. 1st ax."

// ---------------------------------------------------------------------------
// _PATH__: DCS safety zero speed monitoring
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS safety zero speed monitoring
*)
   #endif
                 RD.NOT    DCS_S02              ;"DCS speed set 2"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 WRT       SVZR1__PATH__        ;"zero mon. req. 1st axis"

// ---------------------------------------------------------------------------
// _PATH__: DCS selection of safety speed set 2
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS selection of safety speed set 2
*)
   #endif
                 RD        DCS_S02              ;"DCS speed set 2"
                 WRT       SVA1__PATH__         ;"saf. sel. sig. A 1st axis"
#endif

#if brake_1st_axis__PATH__ == 1
// ---------------------------------------------------------------------------
// _PATH__: management of the brake output
// replace T_BIT000 with the real brake output Yxxx.y
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: management of the brake output
replace T_BIT000 with the real brake output Yxxx.y
*)
   #endif

   #if dcs_support == 0
                 RD        relb_1st__PATH__     ;"release brake 1st axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #else
                 RD        relb_1st__PATH__     ;"release brake 1st axis"
                 AND       *BRK1__PATH__        ;"Safety Brake sig. 1st axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #endif
#endif


