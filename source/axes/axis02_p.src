!!:--------------------------------------------------------------------------:
!!: File              : axis02_{{PATH}}.src                                       :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Management of 2nd axis                               :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:05.11.18: 2.1.0 :Blp    :In case of brake control, the activation of the  :
!!:        :       :       :*IT signal is accelerated when wp coordinate     :
!!:        :       :       :preset is executed or hold to run button is      :
!!:        :       :       :pressed.                                         :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

!! ---------------------------------------------------------------------------
!! {{PATH}}: overtravel check
!! if there are limit switches for the 2nd axis,
!! replace LOG1 with the real input signals Xxxx.y
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: overtravel check
if there are limit switches for the 2nd axis,
replace LOG1 with the real input signals Xxxx.y
*)
#endif
                 RD        LOG1                 ;"logic one"
                 WRT       *+L2_{{PATH}}         ;"overtravel + 1st axis"

                 RD        LOG1                 ;"logic one"
                 WRT       *-L2_{{PATH}}         ;"overtravel - 1st axis"

!! ---------------------------------------------------------------------------
!! reference position return
!! ---------------------------------------------------------------------------
#if ref_dir_2nd_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
   #include "axis02ref_{{PATH}}.src"       !! reference position return 2nd axis
#endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: led 2nd axis selected
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: led 2nd axis selected
*)
#endif
                 RD        M_K2AX_{{PATH}}       ;"memory 2nd axis selected"
#if ref_dir_1st_axis_{{PATH}} > -1
                 AND.NOT   REFRUN_{{PATH}}       ;"reference return active"
   #if ref_dir_2nd_axis_{{PATH}} > 0
                 OR        R_REF2_{{PATH}}       ;"reference return 2nd axis"
   #endif
#endif
                 AND       M_MAN_{{PATH}}        ;"manual mode active"
                 WRT       L_2AX_{{PATH}}        ;"led 2nd axis sel."

!! ---------------------------------------------------------------------------
!! {{PATH}}: jog feed
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: jog feed
*)
#endif
                 RD        M_K2AX_{{PATH}}       ;"memory 2nd axis selected"
                 AND       M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_2nd_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_2p_{{PATH}}      ;"ref: move 2nd axis +dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 2 and spindle_support_P01 > -1
                 AND.NOT   EAX2_{{PATH}}         ;"2nd axis is pmc axis"
#endif
                 WRT       J2+_{{PATH}}          ;"feed 2nd axis + direction"
                 WRT       L_2AXP_{{PATH}}       ;"led 2nd axis + dir."

                 RD        M_K2AX_{{PATH}}       ;"memory 2nd axis selected"
                 AND       M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_2nd_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_2m_{{PATH}}      ;"ref: move 2nd axis -dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 2 and spindle_support_P01 > -1
                 AND.NOT   EAX2_{{PATH}}         ;"2nd axis is pmc axis"
#endif
                 WRT       J2-_{{PATH}}          ;"feed 2nd axis - direction"
                 WRT       L_2AXM_{{PATH}}       ;"led 2nd axis - dir."

!! ---------------------------------------------------------------------------
!! {{PATH}}: workpiece coordinate system preset
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: workpiece coordinate system preset
*)
#endif
                 RD        REQWP2_{{PATH}}       ;"req wp coord preset 2nd axis"
                 AND       WPRST2_{{PATH}}       ;"wp coord. preset 2nd axis"
                 AND       WPSF2_{{PATH}}        ;"wp coord. preset fin 2nd axis"
                 OR        RST_{{PATH}}          ;"resetting signal"
                 RST       REQWP2_{{PATH}}       ;"req wp coord preset 2nd axis"

                 RD        M102_{{PATH}}         ;"wp coord preset 2nd axis"
                 AND       WPRST2_{{PATH}}       ;"wp coord preset 2nd axis"
                 AND       WPSF2_{{PATH}}        ;"wp coord preset fin 2nd ax"
                 RST       M102_{{PATH}}         ;"wp coord preset 2nd axis"

                 RD        FE_RST_{{PATH}}       ;"falling edge reset"
                 AND       wppsreq2_{{PATH}}     ;"req wp coord. preset 2nd ax"
                 RD.STK    FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND.NOT   PGMRUN_{{PATH}}       ;"program running"
                 OR.STK    
                 OR        REQWP2_{{PATH}}       ;"req wp coord preset 2nd axis"
                 OR        M102_{{PATH}}         ;"wp coord preset 2nd axis"
                 RD.STK    WPRST2_{{PATH}}       ;"wp coord. preset 2nd axis"
                 AND.NOT   WPSF2_{{PATH}}        ;"wp coord. preset fin 2nd axis"
                 OR.STK    
                 AND.NOT   RST_{{PATH}}          ;"resetting signal"
                 WRT       WPRST2_{{PATH}}       ;"wp coord. preset 2nd axis"

                 RD        FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND       PGMRUN_{{PATH}}       ;"program running"
                 RD.STK    wppsreq2_{{PATH}}     ;"req wp coord. preset 2nd ax"
                 AND.NOT   FE_RST_{{PATH}}       ;"falling edge reset"
                 OR.STK    
                 WRT       wppsreq2_{{PATH}}     ;"req wp coord. preset 2nd ax"

#if brake_2nd_axis_{{PATH}} == 0
!! ---------------------------------------------------------------------------
!! {{PATH}}: axis interlock
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: axis interlock
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       POW_OK_{{PATH}}       ;"power supply axis ok"
   #else
                 RD        POW_OK_{{PATH}}       ;"power suply axis ok"
   #endif
                 AND.NOT   WPRST2_{{PATH}}       ;"wp coord. preset 2nd axis"
                 WRT       *IT2_{{PATH}}         ;"interlock 2nd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    POW_OK_{{PATH}}       ;"power suply axis ok"
                 WRT       SVF2_{{PATH}}         ;"servo off 2nd axis"
#else
!! ---------------------------------------------------------------------------
!! {{PATH}}: brake control
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: brake control
*)
   #endif
                 RD        POW_OK_{{PATH}}       ;"power supply axis ok"
                 AND       SA2_{{PATH}}          ;"2nd axis servo ready"
                 WRT       relb_2nd_{{PATH}}     ;"release brake 2nd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake released
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake released
*)
   #endif
                 RD        relb_2nd_{{PATH}}     ;"release brake 2nd axis"
                 TMRB      
                           0
                           96
                 WRT       brel_2nd_{{PATH}}     ;"brake 2nd axis released"

   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       brel_2nd_{{PATH}}     ;"brake 2nd axis released"
   #else
                 RD        brel_2nd_{{PATH}}     ;"brake 2nd axis released"
   #endif
                 AND.NOT   WPRST2_{{PATH}}       ;"wp coord preset 2nd axis"
                 WRT       *IT2_{{PATH}}         ;"interlock 2nd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake clamped
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake clamped
*)
   #endif
                 RD.NOT    relb_2nd_{{PATH}}     ;"release brake 2nd axis"
                 TMRB      
                           0
                           48
                 WRT       2nd_soff_{{PATH}}     ;"2nd axis servo off"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    FICYCL               ;"first cycle finished"
                 OR        1st_svf2_{{PATH}}     ;"1st servo off 2nd axis"
                 AND.NOT   POW_OK_{{PATH}}       ;"power supply axis ok"
                 WRT       1st_svf2_{{PATH}}     ;"1st servo off 2nd axis"

                 RD        1st_svf2_{{PATH}}     ;"1st servo off 2nd axis"
                 OR        2nd_soff_{{PATH}}     ;"2nd axis servo off"
                 WRT       SVF2_{{PATH}}         ;"servo off 2nd axis"
#endif
!! ---------------------------------------------------------------------------
!! {{PATH}}: external deceleration
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: external deceleration
*)
#endif
                 RD.NOT    SETUP_{{PATH}}        ;"set-up active"
                 WRT       *+ED2_{{PATH}}        ;"external dec. + 2nd axis"
                 WRT       *-ED2_{{PATH}}        ;"external dec. - 2nd axis"

#if dcs_support == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS MCC off signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS MCC off signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        *MCFV2_{{PATH}}       ;"MCC off signal 2nd axis"
   #endif
                 WRT       M*MCFV2_{{PATH}}      ;"2nd axis MCC off signal"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS monitoring result signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS monitoring result signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        RSV2_{{PATH}}         ;"monitoring result 2nd axis"
   #endif
                 WRT       MRSV2_{{PATH}}        ;"2nd axis monit. result"

!! ---------------------------------------------------------------------------
!! {{PATH}}: position information effect signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
DCS position information effect signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        POSE2_{{PATH}}        ;"pos. inf. effect 2nd axis"
   #endif
                 WRT       MPOSE2_{{PATH}}       ;"2nd axis pos. inf. effect"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety check activation
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety check activation
*)
   #endif
                 RD        *SGOPN               ;"guard state doors closed"
                 WRT       *VLDV2_{{PATH}}       ;"safety check req. 2nd ax."

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety zero speed monitoring
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety zero speed monitoring
*)
   #endif
                 RD.NOT    DCS_S02              ;"DCS speed set 2"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 WRT       SVZR2_{{PATH}}        ;"zero mon. req. 2nd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS selection of safety speed set 2
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS selection of safety speed set 2
*)
   #endif
                 RD        DCS_S02              ;"DCS speed set 2"
                 WRT       SVA2_{{PATH}}         ;"saf. sel. sig. A 2nd axis"
#endif

#if brake_2nd_axis_{{PATH}} == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: management of the brake output
!! replace T_BIT000 with the real brake output Yxxx.y
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: management of the brake output
replace T_BIT000 with the real brake output Yxxx.y
*)
   #endif

   #if dcs_support == 0
                 RD        relb_2nd_{{PATH}}     ;"release brake 2nd axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #else
                 RD        relb_2nd_{{PATH}}     ;"release brake 2nd axis"
                 AND       *BRK2_{{PATH}}        ;"Safety Brake sig. 2nd axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #endif
#endif


