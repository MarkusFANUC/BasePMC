//:--------------------------------------------------------------------------:
//: File              : axis02ref__PATH__.src                                    :
//: Creation date     : 16.09.2013                                           :
//: Last modification : see table below                                      :
//: Author            : FANUC FA Switzerland / Peter Bloesch                 :
//: Description       : Reference position return 2nd axis                   :
//:--------------------------------------------------------------------------:
//
//:--------------------------------------------------------------------------:
//:                     Modification                                         :
//:--------------------------------------------------------------------------:
//: Date   :Version: Sign  :Description                                      :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:

// ===========================================================================
// Code
// ===========================================================================

#if ref_dir_2nd_axis__PATH__ == 5 || ref_dir_2nd_axis__PATH__ == 6

// ---------------------------------------------------------------------------
// 2nd axis reference position return in plus direction without dog
// parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 0 (ZMI)
// ---------------------------------------------------------------------------
   #if ref_dir_2nd_axis__PATH__ == 5
      #if net_comment == 1
(*
_PATH__: 2nd axis reference position return in plus direction
without dog
parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 0 (ZMI)
*)
      #endif
   #endif

// ---------------------------------------------------------------------------
// 2nd axis reference position return in minus direction without dog
// parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 1 (ZMI)
// ---------------------------------------------------------------------------
   #if ref_dir_2nd_axis__PATH__ == 6
(*
_PATH__: 2nd axis reference position return in minus direction
without dog
parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 1 (ZMI)
*)
   #endif

// ---------------------------------------------------------------------------
// _PATH__: timer to specify the time for one motor revolution
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: timer to specify the time for one motor revolution
*)
   #endif
                 RD        R_REF2__PATH__       ;"reference return 2nd axis"
                 AND       M_REFJ__PATH__       ;"jog mode during ref return"
                 AND       MV2__PATH__          ;"2nd axis moving signal"
                 TMRC      
                           5
                           psdog2__PATH__       ;"preset soft. dog 2nd axis"
                           csdog2__PATH__       ;"counter soft. dog 2nd axis"
                 SET       inctr2__PATH__       ;"increment counter 2nd axis"

                 RD        REFSTP__PATH__       ;"stop reference return"
                 OR        ZP2__PATH__          ;"ref position 2nd axis"
                 AND       inctr2__PATH__       ;"increment counter 2nd axis"
                 RST       inctr2__PATH__       ;"increment counter 2nd axis"

// ---------------------------------------------------------------------------
// _PATH__: 2nd axis reference position return
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: 2nd axis reference position return
*)
   #endif
                 RD        M_KPLUS__PATH__      ;"memory axis plus dir."
                 OR        M_KMINUS__PATH__     ;"memory axis minus dir."
                 AND       M_K2AX__PATH__       ;"memory 2nd axis selected"
                 AND.NOT   AUTREF__PATH__       ;"automatic refernce return"
                 RD.STK    A_REF2__PATH__       ;"auto ref. return 2nd axis"
                 OR        R_REF2__PATH__       ;"reference return 2nd axis"
                 AND.NOT   ZP2__PATH__          ;"ref position 2nd axis"
                 OR.STK    
                 AND.NOT   REFSTP__PATH__       ;"stop reference return"
                 AND.NOT   NOREF2__PATH__       ;"no reference return 2nd axis"
                 WRT       R_REF2__PATH__       ;"reference return 2nd axis"

                 RD        R_REF2__PATH__       ;"reference return 2nd axis"
                 AND.NOT   REFSTP__PATH__       ;"stop reference return"
                 AND.NOT   inctr2__PATH__       ;"increment counter 2nd axis"
                 SET       D_REF2__PATH__       ;"2nd axis ref without dog"

                 RD        inctr2__PATH__       ;"increment counter 2nd axis"
                 OR.NOT    R_REF2__PATH__       ;"reference return 2nd axis"
                 OR        REFSTP__PATH__       ;"stop reference return"
                 RST       D_REF2__PATH__       ;"2nd axis ref without dog"

                 RD        M_REFJ__PATH__       ;"jog mode during ref return"
                 AND       D_REF2__PATH__       ;"2nd axis ref without dog"
                 AND.NOT   inctr2__PATH__       ;"increment counter 2nd axis"
                 RD.STK    M_REF__PATH__        ;"mode REF selected"
                 AND.NOT   M_REFJ__PATH__       ;"jog mode during ref return"
                 AND       inctr2__PATH__       ;"increment counter 2nd axis"
                 OR.STK    
                 AND       R_REF2__PATH__       ;"reference return 2nd axis"
                 AND       D_JOGMV__PATH__      ;"delay jog move"
   #if ref_dir_2nd_axis__PATH__ == 5
                 WRT       aref_2p__PATH__      ;"ref: move 2nd axis +dir."
   #endif
   #if ref_dir_2nd_axis__PATH__ == 6
                 WRT       aref_2m__PATH__      ;"ref: move 2nd axis -dir."
   #endif

#else
// ---------------------------------------------------------------------------
// _PATH__: in case of using the reference switch for the 1st axis,
// reference position return of the 2nd axis
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: in case of using the reference switch for the 2nd axis,
replace the contact 500ms with the real input signal
*)
   #endif
                 RD        500MS                ;"500 ms"
                 WRT       m_iref2__PATH__      ;"mem ref. switch 2nd axis"

// ---------------------------------------------------------------------------
// counter to specify the lenght of the deceleration dog for the
// reference position return of the 2nd axis
// ---------------------------------------------------------------------------
   #if net_comment == 1
      #if ref_dir_2nd_axis__PATH__ == 1 || ref_dir_2nd_axis__PATH__ == 2
(*
_PATH__: counter to specify the lenght of the deceleration dog for
the reference position return of the 2nd axis (contact: NC)
*)
      #endif
      #if ref_dir_2nd_axis__PATH__ == 3 || ref_dir_2nd_axis__PATH__ == 4
(*
_PATH__: counter to specify the lenght of the deceleration dog for
the reference position return of the 2nd axis (contact: NO)
*)
      #endif
   #endif
                 RD        LOG1                 ;"logic one"
                 RD.STK.NOT LOG1                ;"logic one"
   #if ref_dir_2nd_axis__PATH__ == 1 || ref_dir_2nd_axis__PATH__ == 2
                 RD.STK    m_iref2__PATH__      ;"mem ref. switch 2nd axis"
   #endif
   #if ref_dir_2nd_axis__PATH__ == 3 || ref_dir_2nd_axis__PATH__ == 4
                 RD.STK.NOT m_iref2__PATH__      ;"mem ref. switch 2nd axis"
   #endif
                 RD.STK    CLOCK                ;"cycle clock pulse"
                 AND       MV2__PATH__          ;"2nd axis moving signal"
                 CTRC
                           psdog2__PATH__       ;"preset soft. dog 2nd axis"
                           csdog2__PATH__       ;"counter soft. dog 2nd axis"
                 WRT       inctr2__PATH__       ;"increment counter 2nd axis"

                 RD        inctr2__PATH__       ;"increment counter 2nd axis"
                 RD.STK    esdog2__PATH__       ;"end soft dec. dog 2nd axis"
   #if ref_dir_2nd_axis__PATH__ == 1 || ref_dir_2nd_axis__PATH__ == 2 
                 AND.NOT   m_iref2__PATH__      ;"mem ref. switch 2nd axis"
   #endif
   #if ref_dir_2nd_axis__PATH__ == 3 || ref_dir_2nd_axis__PATH__ == 4
                 AND       m_iref2__PATH__      ;"mem ref. switch 2nd axis"
   #endif
                 OR.STK
                 WRT       esdog2__PATH__       ;"end soft dec. dog 2nd axis"

// ---------------------------------------------------------------------------
// 2nd axis reference position return
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: 2nd axis reference position return
*)
   #endif
                 RD        M_KPLUS__PATH__      ;"memory axis plus dir."
                 OR        M_KMINUS__PATH__     ;"memory axis minus dir."
                 AND       M_K2AX__PATH__       ;"memory 2nd axis selected"
                 AND.NOT   AUTREF__PATH__       ;"automatic reference return"
                 RD.STK    A_REF2__PATH__       ;"auto ref. return 2nd axis"
                 OR        R_REF2__PATH__       ;"reference return 2nd axis"
                 AND.NOT   ZP2__PATH__          ;"ref position 2nd axis"
                 OR.STK
                 AND.NOT   REFSTP__PATH__       ;"stop reference return"
                 AND.NOT   NOREF2__PATH__       ;"no reference return 2nd axis"
                 WRT       R_REF2__PATH__       ;"reference return 2nd axis"

// ---------------------------------------------------------------------------
// 2nd axis reference position return in plus direction
// parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
// 3003 bit 5 = 0 (DEC), 3006 bit 0 = 1 (GDC)
// ---------------------------------------------------------------------------
   #if ref_dir_2nd_axis__PATH__ == 1 || ref_dir_2nd_axis__PATH__ == 3 
      #if net_comment == 1
         #if ref_dir_2nd_axis__PATH__ == 1
(*
_PATH__: 2nd axis reference position return in plus direction
(contact: NC, dog at negative axis limit)
parameter setting:  1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
         #if ref_dir_2nd_axis__PATH__ == 3
(*
_PATH__: 2nd axis reference position return in plus direction
(contact: NO, dog at negative axis limit)
parameter setting:  1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
      #endif
                 RD        R_REF2__PATH__       ;"reference return 2nd axis"
      #if ref_dir_2nd_axis__PATH__ == 1
                 AND.NOT   m_iref2__PATH__      ;"mem ref. switch 2nd axis"
      #endif
      #if ref_dir_2nd_axis__PATH__ == 3
                 AND       m_iref2__PATH__      ;"mem ref. switch 2nd axis"
      #endif
                 AND.NOT   aref_2m__PATH__      ;"ref: move 2nd axis -dir."
                 WRT       aref_2p__PATH__      ;"ref: move 2nd axis +dir."

                 RD        R_REF2__PATH__       ;"reference return 2nd axis"
                 AND.NOT   aref_2p__PATH__      ;"ref: move 2nd axis +dir."
                 WRT       aref_2m__PATH__      ;"ref: move 2nd axis -dir."
      #if ref_dir_2nd_axis__PATH__ == 1
                 RD        m_iref2__PATH__      ;"mem ref. switch 2nd axis"
      #endif
      #if ref_dir_2nd_axis__PATH__ == 3
                 RD.NOT    m_iref2__PATH__      ;"mem ref. switch 2nd axis"
      #endif
                 RD.STK    esdog2__PATH__       ;"end soft dec. dog 2nd axis"
                 AND       MVD2__PATH__         ;"2nd axis moving dir. signal"
                 OR.STK
                 OR        aref_2p__PATH__      ;"ref: move 2nd axis +dir."
                 WRT       *DEC2__PATH__        ;"deceleration signal 2nd axis"
   #endif

// ---------------------------------------------------------------------------
// 2nd axis reference position return in minus direction
// parameter setting:  1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
// 3003 bit 5 = 0 (DEC), 3006 bit 0 = 1 (GDC)
// ---------------------------------------------------------------------------
   #if ref_dir_2nd_axis__PATH__ == 2 || ref_dir_2nd_axis__PATH__ == 4
      #if net_comment == 1
         #if ref_dir_2nd_axis__PATH__ == 2
(*
_PATH__: 2nd axis reference position return in minus direction
(contact: NC, dog at positive axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
         #if ref_dir_2nd_axis__PATH__ == 4
(*
_PATH__: 2nd axis reference position return in minus direction
(contact: NO, dog at positive axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
      #endif
                 RD        R_REF2__PATH__       ;"reference return 2nd axis"
      #if ref_dir_2nd_axis__PATH__ == 2
                 AND.NOT   m_iref2__PATH__      ;"mem ref. switch 2nd axis"
      #endif
      #if ref_dir_2nd_axis__PATH__ == 4
                 AND       m_iref2__PATH__      ;"mem ref. switch 2nd axis"
      #endif
                 AND.NOT   aref_2p__PATH__      ;"ref: move 2nd axis +dir."
                 WRT       aref_2m__PATH__      ;"ref: move 2nd axis -dir."

                 RD        R_REF2__PATH__       ;"reference return 2nd axis"
                 AND.NOT   aref_2m__PATH__      ;"ref: move 2nd axis -dir."
                 WRT       aref_2p__PATH__      ;"ref: move 2nd axis +dir."
      #if ref_dir_2nd_axis__PATH__ == 2
                 RD        m_iref2__PATH__      ;"mem ref. switch 2nd axis"
      #endif
      #if ref_dir_2nd_axis__PATH__ == 4
                 RD.NOT    m_iref2__PATH__      ;"mem ref. switch 2nd axis"
      #endif
                 RD.STK    esdog2__PATH__       ;"end soft dec. dog 2nd axis"
                 AND.NOT   MVD2__PATH__         ;"2nd axis moving dir. signal"
                 OR.STK
                 OR        aref_2m__PATH__      ;"ref: move 2nd axis -dir."
                 WRT       *DEC2__PATH__        ;"deceleration signal 2nd axis"
   #endif

#endif