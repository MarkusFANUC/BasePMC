!!:--------------------------------------------------------------------------:
!!: File              : axis02ref_{{PATH}}.src                                    :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Reference position return 2nd axis                   :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

#if ref_dir_2nd_axis_{{PATH}} == 5 or ref_dir_2nd_axis_{{PATH}} == 6

!! ---------------------------------------------------------------------------
!! 2nd axis reference position return in plus direction without dog
!! parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 0 (ZMI)
!! ---------------------------------------------------------------------------
   #if ref_dir_2nd_axis_{{PATH}} == 5
      #if net_comment == 1
(*
{{PATH}}: 2nd axis reference position return in plus direction
without dog
parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 0 (ZMI)
*)
      #endif
   #endif

!! ---------------------------------------------------------------------------
!! 2nd axis reference position return in minus direction without dog
!! parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 1 (ZMI)
!! ---------------------------------------------------------------------------
   #if ref_dir_2nd_axis_{{PATH}} == 6
(*
{{PATH}}: 2nd axis reference position return in minus direction
without dog
parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 1 (ZMI)
*)
   #endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: timer to specify the time for one motor revolution
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: timer to specify the time for one motor revolution
*)
   #endif
                 RD        R_REF2_{{PATH}}       ;"reference return 2nd axis"
                 AND       M_REFJ_{{PATH}}       ;"jog mode during ref return"
                 AND       MV2_{{PATH}}          ;"2nd axis moving signal"
                 TMRC      
                           5
                           psdog2_{{PATH}}       ;"preset soft. dog 2nd axis"
                           csdog2_{{PATH}}       ;"counter soft. dog 2nd axis"
                 SET       inctr2_{{PATH}}       ;"increment counter 2nd axis"

                 RD        REFSTP_{{PATH}}       ;"stop reference return"
                 OR        ZP2_{{PATH}}          ;"ref position 2nd axis"
                 AND       inctr2_{{PATH}}       ;"increment counter 2nd axis"
                 RST       inctr2_{{PATH}}       ;"increment counter 2nd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: 2nd axis reference position return
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: 2nd axis reference position return
*)
   #endif
                 RD        M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 OR        M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND       M_K2AX_{{PATH}}       ;"memory 2nd axis selected"
                 AND.NOT   AUTREF_{{PATH}}       ;"automatic refernce return"
                 RD.STK    A_REF2_{{PATH}}       ;"auto ref. return 2nd axis"
                 OR        R_REF2_{{PATH}}       ;"reference return 2nd axis"
                 AND.NOT   ZP2_{{PATH}}          ;"ref position 2nd axis"
                 OR.STK    
                 AND.NOT   REFSTP_{{PATH}}       ;"stop reference return"
                 AND.NOT   NOREF2_{{PATH}}       ;"no reference return 2nd axis"
                 WRT       R_REF2_{{PATH}}       ;"reference return 2nd axis"

                 RD        R_REF2_{{PATH}}       ;"reference return 2nd axis"
                 AND.NOT   REFSTP_{{PATH}}       ;"stop reference return"
                 AND.NOT   inctr2_{{PATH}}       ;"increment counter 2nd axis"
                 SET       D_REF2_{{PATH}}       ;"2nd axis ref without dog"

                 RD        inctr2_{{PATH}}       ;"increment counter 2nd axis"
                 OR.NOT    R_REF2_{{PATH}}       ;"reference return 2nd axis"
                 OR        REFSTP_{{PATH}}       ;"stop reference return"
                 RST       D_REF2_{{PATH}}       ;"2nd axis ref without dog"

                 RD        M_REFJ_{{PATH}}       ;"jog mode during ref return"
                 AND       D_REF2_{{PATH}}       ;"2nd axis ref without dog"
                 AND.NOT   inctr2_{{PATH}}       ;"increment counter 2nd axis"
                 RD.STK    M_REF_{{PATH}}        ;"mode REF selected"
                 AND.NOT   M_REFJ_{{PATH}}       ;"jog mode during ref return"
                 AND       inctr2_{{PATH}}       ;"increment counter 2nd axis"
                 OR.STK    
                 AND       R_REF2_{{PATH}}       ;"reference return 2nd axis"
                 AND       D_JOGMV_{{PATH}}      ;"delay jog move"
   #if ref_dir_2nd_axis_{{PATH}} == 5
                 WRT       aref_2p_{{PATH}}      ;"ref: move 2nd axis +dir."
   #endif
   #if ref_dir_2nd_axis_{{PATH}} == 6
                 WRT       aref_2m_{{PATH}}      ;"ref: move 2nd axis -dir."
   #endif

#else
!! ---------------------------------------------------------------------------
!! {{PATH}}: in case of using the reference switch for the 1st axis,
!! reference position return of the 2nd axis
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: in case of using the reference switch for the 2nd axis,
replace the contact 500ms with the real input signal
*)
   #endif
                 RD        500MS                ;"500 ms"
                 WRT       m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"

!! ---------------------------------------------------------------------------
!! counter to specify the lenght of the deceleration dog for the
!! reference position return of the 2nd axis
!! ---------------------------------------------------------------------------
   #if net_comment == 1
      #if ref_dir_2nd_axis_{{PATH}} == 1 or ref_dir_2nd_axis_{{PATH}} == 2
(*
{{PATH}}: counter to specify the lenght of the deceleration dog for
the reference position return of the 2nd axis (contact: NC)
*)
      #endif
      #if ref_dir_2nd_axis_{{PATH}} == 3 or ref_dir_2nd_axis_{{PATH}} == 4
(*
{{PATH}}: counter to specify the lenght of the deceleration dog for
the reference position return of the 2nd axis (contact: NO)
*)
      #endif
   #endif
                 RD        LOG1                 ;"logic one"
                 RD.STK.NOT LOG1                ;"logic one"
   #if ref_dir_2nd_axis_{{PATH}} == 1 or ref_dir_2nd_axis_{{PATH}} == 2
                 RD.STK    m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
   #endif
   #if ref_dir_2nd_axis_{{PATH}} == 3 or ref_dir_2nd_axis_{{PATH}} == 4
                 RD.STK.NOT m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
   #endif
                 RD.STK    CLOCK                ;"cycle clock pulse"
                 AND       MV2_{{PATH}}          ;"2nd axis moving signal"
                 CTRC
                           psdog2_{{PATH}}       ;"preset soft. dog 2nd axis"
                           csdog2_{{PATH}}       ;"counter soft. dog 2nd axis"
                 WRT       inctr2_{{PATH}}       ;"increment counter 2nd axis"

                 RD        inctr2_{{PATH}}       ;"increment counter 2nd axis"
                 RD.STK    esdog2_{{PATH}}       ;"end soft dec. dog 2nd axis"
   #if ref_dir_2nd_axis_{{PATH}} == 1 or ref_dir_2nd_axis_{{PATH}} == 2 
                 AND.NOT   m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
   #endif
   #if ref_dir_2nd_axis_{{PATH}} == 3 or ref_dir_2nd_axis_{{PATH}} == 4
                 AND       m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
   #endif
                 OR.STK
                 WRT       esdog2_{{PATH}}       ;"end soft dec. dog 2nd axis"

!! ---------------------------------------------------------------------------
!! 2nd axis reference position return
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: 2nd axis reference position return
*)
   #endif
                 RD        M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 OR        M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND       M_K2AX_{{PATH}}       ;"memory 2nd axis selected"
                 AND.NOT   AUTREF_{{PATH}}       ;"automatic reference return"
                 RD.STK    A_REF2_{{PATH}}       ;"auto ref. return 2nd axis"
                 OR        R_REF2_{{PATH}}       ;"reference return 2nd axis"
                 AND.NOT   ZP2_{{PATH}}          ;"ref position 2nd axis"
                 OR.STK
                 AND.NOT   REFSTP_{{PATH}}       ;"stop reference return"
                 AND.NOT   NOREF2_{{PATH}}       ;"no reference return 2nd axis"
                 WRT       R_REF2_{{PATH}}       ;"reference return 2nd axis"

!! ---------------------------------------------------------------------------
!! 2nd axis reference position return in plus direction
!! parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
!! 3003 bit 5 = 0 (DEC), 3006 bit 0 = 1 (GDC)
!! ---------------------------------------------------------------------------
   #if ref_dir_2nd_axis_{{PATH}} == 1 or ref_dir_2nd_axis_{{PATH}} == 3 
      #if net_comment == 1
         #if ref_dir_2nd_axis_{{PATH}} == 1
(*
{{PATH}}: 2nd axis reference position return in plus direction
(contact: NC, dog at negative axis limit)
parameter setting:  1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
         #if ref_dir_2nd_axis_{{PATH}} == 3
(*
{{PATH}}: 2nd axis reference position return in plus direction
(contact: NO, dog at negative axis limit)
parameter setting:  1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
      #endif
                 RD        R_REF2_{{PATH}}       ;"reference return 2nd axis"
      #if ref_dir_2nd_axis_{{PATH}} == 1
                 AND.NOT   m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
      #endif
      #if ref_dir_2nd_axis_{{PATH}} == 3
                 AND       m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
      #endif
                 AND.NOT   aref_2m_{{PATH}}      ;"ref: move 2nd axis -dir."
                 WRT       aref_2p_{{PATH}}      ;"ref: move 2nd axis +dir."

                 RD        R_REF2_{{PATH}}       ;"reference return 2nd axis"
                 AND.NOT   aref_2p_{{PATH}}      ;"ref: move 2nd axis +dir."
                 WRT       aref_2m_{{PATH}}      ;"ref: move 2nd axis -dir."
      #if ref_dir_2nd_axis_{{PATH}} == 1
                 RD        m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
      #endif
      #if ref_dir_2nd_axis_{{PATH}} == 3
                 RD.NOT    m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
      #endif
                 RD.STK    esdog2_{{PATH}}       ;"end soft dec. dog 2nd axis"
                 AND       MVD2_{{PATH}}         ;"2nd axis moving dir. signal"
                 OR.STK
                 OR        aref_2p_{{PATH}}      ;"ref: move 2nd axis +dir."
                 WRT       *DEC2_{{PATH}}        ;"deceleration signal 2nd axis"
   #endif

!! ---------------------------------------------------------------------------
!! 2nd axis reference position return in minus direction
!! parameter setting:  1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
!! 3003 bit 5 = 0 (DEC), 3006 bit 0 = 1 (GDC)
!! ---------------------------------------------------------------------------
   #if ref_dir_2nd_axis_{{PATH}} == 2 or ref_dir_2nd_axis_{{PATH}} == 4
      #if net_comment == 1
         #if ref_dir_2nd_axis_{{PATH}} == 2
(*
{{PATH}}: 2nd axis reference position return in minus direction
(contact: NC, dog at positive axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
         #if ref_dir_2nd_axis_{{PATH}} == 4
(*
{{PATH}}: 2nd axis reference position return in minus direction
(contact: NO, dog at positive axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
      #endif
                 RD        R_REF2_{{PATH}}       ;"reference return 2nd axis"
      #if ref_dir_2nd_axis_{{PATH}} == 2
                 AND.NOT   m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
      #endif
      #if ref_dir_2nd_axis_{{PATH}} == 4
                 AND       m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
      #endif
                 AND.NOT   aref_2p_{{PATH}}      ;"ref: move 2nd axis +dir."
                 WRT       aref_2m_{{PATH}}      ;"ref: move 2nd axis -dir."

                 RD        R_REF2_{{PATH}}       ;"reference return 2nd axis"
                 AND.NOT   aref_2m_{{PATH}}      ;"ref: move 2nd axis -dir."
                 WRT       aref_2p_{{PATH}}      ;"ref: move 2nd axis +dir."
      #if ref_dir_2nd_axis_{{PATH}} == 2
                 RD        m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
      #endif
      #if ref_dir_2nd_axis_{{PATH}} == 4
                 RD.NOT    m_iref2_{{PATH}}      ;"mem ref. switch 2nd axis"
      #endif
                 RD.STK    esdog2_{{PATH}}       ;"end soft dec. dog 2nd axis"
                 AND.NOT   MVD2_{{PATH}}         ;"2nd axis moving dir. signal"
                 OR.STK
                 OR        aref_2m_{{PATH}}      ;"ref: move 2nd axis -dir."
                 WRT       *DEC2_{{PATH}}        ;"deceleration signal 2nd axis"
   #endif

#endif