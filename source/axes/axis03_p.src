!!:--------------------------------------------------------------------------:
!!: File              : axis03_{{PATH}}.src                                       :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Management of 3rd axis                               :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:05.11.18: 2.1.0 :Blp    :In case of brake control, the activation of the  :
!!:        :       :       :*IT signal is accelerated when wp coordinate     :
!!:        :       :       :preset is executed or hold to run button is      :
!!:        :       :       :pressed.                                         :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

!! ---------------------------------------------------------------------------
!! {{PATH}}: overtravel check
!! if there are limit switches for the 3rd axis,
!! replace LOG1 with the real input signals Xxxx.y
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: overtravel check
if there are limit switches for the 3rd axis,
replace LOG1 with the real input signals Xxxx.y
*)
#endif
                 RD        LOG1                 ;"logic one"
                 WRT       *+L3_{{PATH}}         ;"overtravel + 3rd axis"

                 RD        LOG1                 ;"logic one"
                 WRT       *-L3_{{PATH}}         ;"overtravel - 3rd axis"

!! ---------------------------------------------------------------------------
!! reference position return
!! ---------------------------------------------------------------------------
#if ref_dir_3rd_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
   #include "axis03ref_{{PATH}}.src"       !! reference position return 3rd axis
#endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: led 3rd axis selected
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: led 3rd axis selected
*)
#endif
                 RD        M_K3AX_{{PATH}}       ;"memory 3rd axis selected"
#if ref_dir_1st_axis_{{PATH}} > -1
                 AND.NOT   REFRUN_{{PATH}}       ;"reference return active"
   #if ref_dir_3rd_axis_{{PATH}} > 0
                 OR        R_REF3_{{PATH}}       ;"reference return 3rd axis"
   #endif
#endif
                 AND       M_MAN_{{PATH}}        ;"manual mode active"
                 WRT       L_3AX_{{PATH}}        ;"led 3rd axis sel."

!! ---------------------------------------------------------------------------
!! {{PATH}}: jog feed
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: jog feed
*)
#endif
                 RD        M_K3AX_{{PATH}}       ;"memory 3rd axis selected"
                 AND       M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_3rd_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_3p_{{PATH}}      ;"ref: move 3rd axis +dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 3 and spindle_support_P01 > -1
                 AND.NOT   EAX3_{{PATH}}         ;"3rd axis is pmc axis"
#endif
                 WRT       J3+_{{PATH}}          ;"feed 3rd axis + direction"
                 WRT       L_3AXP_{{PATH}}       ;"led 3rd axis + dir."

                 RD        M_K3AX_{{PATH}}       ;"memory 3rd axis selected"
                 AND       M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_3rd_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_3m_{{PATH}}      ;"ref: move 3rd axis -dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 3 and spindle_support_P01 > -1
                 AND.NOT   EAX3_{{PATH}}         ;"3rd axis is pmc axis"
#endif
                 WRT       J3-_{{PATH}}          ;"feed 3rd axis - direction"
                 WRT       L_3AXM_{{PATH}}       ;"led 3rd axis - dir."

!! ---------------------------------------------------------------------------
!! {{PATH}}: workpiece coordinate system preset
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: workpiece coordinate system preset
*)
#endif
                 RD        REQWP3_{{PATH}}       ;"req wp coord preset 3rd axis"
                 AND       WPRST3_{{PATH}}       ;"wp coord. preset 3rd axis"
                 AND       WPSF3_{{PATH}}        ;"wp coord. preset fin 3rd axis"
                 OR        RST_{{PATH}}          ;"resetting signal"
                 RST       REQWP3_{{PATH}}       ;"req wp coord preset 3rd axis"

                 RD        M103_{{PATH}}         ;"wp coord preset 3rd axis"
                 AND       WPRST3_{{PATH}}       ;"wp coord preset 3rd axis"
                 AND       WPSF3_{{PATH}}        ;"wp coord preset fin 3rd ax"
                 RST       M103_{{PATH}}         ;"wp coord preset 3rd axis"

                 RD        FE_RST_{{PATH}}       ;"falling edge reset"
                 AND       wppsreq3_{{PATH}}     ;"req wp coord. preset 3rd ax"
                 RD.STK    FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND.NOT   PGMRUN_{{PATH}}       ;"program running"
                 OR.STK    
                 OR        REQWP3_{{PATH}}       ;"req wp coord preset 3rd axis"
                 OR        M103_{{PATH}}         ;"wp coord preset 3rd axis"
                 RD.STK    WPRST3_{{PATH}}       ;"wp coord. preset 3rd axis"
                 AND.NOT   WPSF3_{{PATH}}        ;"wp coord. preset fin 3rd axis"
                 OR.STK    
                 AND.NOT   RST_{{PATH}}          ;"resetting signal"
                 WRT       WPRST3_{{PATH}}       ;"wp coord. preset 3rd axis"

                 RD        FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND       PGMRUN_{{PATH}}       ;"program running"
                 RD.STK    wppsreq3_{{PATH}}     ;"req wp coord. preset 3rd ax"
                 AND.NOT   FE_RST_{{PATH}}       ;"falling edge reset"
                 OR.STK    
                 WRT       wppsreq3_{{PATH}}     ;"req wp coord. preset 3rd ax"

#if brake_3rd_axis_{{PATH}} == 0
!! ---------------------------------------------------------------------------
!! {{PATH}}: axis interlock
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: axis interlock
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       POW_OK_{{PATH}}       ;"power supply axis ok"
   #else
                 RD        POW_OK_{{PATH}}       ;"power suply axis ok"
   #endif
                 AND.NOT   WPRST3_{{PATH}}       ;"wp coord. preset 3rd axis"
                 WRT       *IT3_{{PATH}}         ;"interlock 3rd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    POW_OK_{{PATH}}       ;"power suply axis ok"
                 WRT       SVF3_{{PATH}}         ;"servo off 3rd axis"
#else
!! ---------------------------------------------------------------------------
!! {{PATH}}: brake control
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: brake control
*)
   #endif
                 RD        POW_OK_{{PATH}}       ;"power supply axis ok"
                 AND       SA3_{{PATH}}          ;"3rd axis servo ready"
                 WRT       relb_3rd_{{PATH}}     ;"release brake 3rd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake released
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake released
*)
   #endif
                 RD        relb_3rd_{{PATH}}     ;"release brake 3rd axis"
                 TMRB      
                           0
                           96
                 WRT       brel_3rd_{{PATH}}     ;"brake 3rd axis released"

   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       brel_3rd_{{PATH}}     ;"brake 3rd axis released"
   #else
                 RD        brel_3rd_{{PATH}}     ;"brake 3rd axis released"
   #endif
                 AND.NOT   WPRST3_{{PATH}}       ;"wp coord preset 3rd axis"
                 WRT       *IT3_{{PATH}}         ;"interlock 3rd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake clamped
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake clamped
*)
   #endif
                 RD.NOT    relb_3rd_{{PATH}}     ;"release brake 3rd axis"
                 TMRB      
                           0
                           48
                 WRT       3rd_soff_{{PATH}}     ;"3rd axis servo off"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    FICYCL               ;"first cycle finished"
                 OR        1st_svf3_{{PATH}}     ;"1st servo off 3rd axis"
                 AND.NOT   POW_OK_{{PATH}}       ;"power supply axis ok"
                 WRT       1st_svf3_{{PATH}}     ;"1st servo off 3rd axis"

                 RD        1st_svf3_{{PATH}}     ;"1st servo off 3rd axis"
                 OR        3rd_soff_{{PATH}}     ;"3rd axis servo off"
                 WRT       SVF3_{{PATH}}         ;"servo off 3rd axis"
#endif
!! ---------------------------------------------------------------------------
!! {{PATH}}: external deceleration
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: external deceleration
*)
#endif
                 RD.NOT    SETUP_{{PATH}}        ;"set-up active"
                 WRT       *+ED3_{{PATH}}        ;"external dec. + 3rd axis"
                 WRT       *-ED3_{{PATH}}        ;"external dec. - 3rd axis"

#if dcs_support == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS MCC off signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS MCC off signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        *MCFV3_{{PATH}}       ;"MCC off signal 3rd axis"
   #endif
                 WRT       M*MCFV3_{{PATH}}      ;"3rd axis MCC off signal"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS monitoring result signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS monitoring result signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        RSV3_{{PATH}}         ;"monitoring result 3rd axis"
   #endif
                 WRT       MRSV3_{{PATH}}        ;"3rd axis monit. result"

!! ---------------------------------------------------------------------------
!! {{PATH}}: position information effect signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
DCS position information effect signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        POSE3_{{PATH}}        ;"pos. inf. effect 3rd axis"
   #endif
                 WRT       MPOSE3_{{PATH}}       ;"3rd axis pos. inf. effect"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety check activation
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety check activation
*)
   #endif
                 RD        *SGOPN               ;"guard state doors closed"
                 WRT       *VLDV3_{{PATH}}       ;"safety check req. 3rd ax."

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety zero speed monitoring
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety zero speed monitoring
*)
   #endif
                 RD.NOT    DCS_S02              ;"DCS speed set 2"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 WRT       SVZR3_{{PATH}}        ;"zero mon. req. 3rd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS selection of safety speed set 2
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS selection of safety speed set 2
*)
   #endif
                 RD        DCS_S02              ;"DCS speed set 2"
                 WRT       SVA3_{{PATH}}         ;"saf. sel. sig. A 3rd axis"
#endif

#if brake_3rd_axis_{{PATH}} == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: management of the brake output
!! replace T_BIT000 with the real brake output Yxxx.y
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: management of the brake output
replace T_BIT000 with the real brake output Yxxx.y
*)
   #endif

   #if dcs_support == 0
                 RD        relb_3rd_{{PATH}}     ;"release brake 3rd axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #else
                 RD        relb_3rd_{{PATH}}     ;"release brake 3rd axis"
                 AND       *BRK3_{{PATH}}        ;"Safety Brake sig. 3rd axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #endif
#endif
