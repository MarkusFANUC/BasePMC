!!:--------------------------------------------------------------------------:
!!: File              : axis03ref_{{PATH}}.src                                    :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Reference position return 3rd axis                   :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

#if ref_dir_3rd_axis_{{PATH}} == 5 or ref_dir_3rd_axis_{{PATH}} == 6

!! ---------------------------------------------------------------------------
!! 3rd axis reference position return in plus direction without dog
!! parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 0 (ZMI)
!! ---------------------------------------------------------------------------
   #if ref_dir_3rd_axis_{{PATH}} == 5
      #if net_comment == 1
(*
{{PATH}}: 3rd axis reference position return in plus direction
without dog
parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 0 (ZMI)
*)
      #endif
   #endif

!! ---------------------------------------------------------------------------
!! 3rd axis reference position return in minus direction without dog
!! parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 1 (ZMI)
!! ---------------------------------------------------------------------------
   #if ref_dir_3rd_axis_{{PATH}} == 6
(*
{{PATH}}: 3rd axis reference position return in minus direction
without dog
parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 1 (ZMI)
*)
   #endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: timer to specify the time for one motor revolution
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: timer to specify the time for one motor revolution
*)
   #endif
                 RD        R_REF3_{{PATH}}       ;"reference return 3rd axis"
                 AND       M_REFJ_{{PATH}}       ;"jog mode during ref return"
                 AND       MV3_{{PATH}}          ;"3rd axis moving signal"
                 TMRC      
                           5
                           psdog3_{{PATH}}       ;"preset soft. dog 3rd axis"
                           csdog3_{{PATH}}       ;"counter soft. dog 3rd axis"
                 SET       inctr3_{{PATH}}       ;"increment counter 3rd axis"

                 RD        REFSTP_{{PATH}}       ;"stop reference return"
                 OR        ZP3_{{PATH}}          ;"ref position 3rd axis"
                 AND       inctr3_{{PATH}}       ;"increment counter 3rd axis"
                 RST       inctr3_{{PATH}}       ;"increment counter 3rd axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: 3rd axis reference position return
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: 3rd axis reference position return
*)
   #endif
                 RD        M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 OR        M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND       M_K3AX_{{PATH}}       ;"memory 3rd axis selected"
                 AND.NOT   AUTREF_{{PATH}}       ;"automatic refernce return"
                 RD.STK    A_REF3_{{PATH}}       ;"auto ref. return 3rd axis"
                 OR        R_REF3_{{PATH}}       ;"reference return 3rd axis"
                 AND.NOT   ZP3_{{PATH}}          ;"ref position 3rd axis"
                 OR.STK    
                 AND.NOT   REFSTP_{{PATH}}       ;"stop reference return"
                 AND.NOT   NOREF3_{{PATH}}       ;"no reference return 3rd axis"
                 WRT       R_REF3_{{PATH}}       ;"reference return 3rd axis"

                 RD        R_REF3_{{PATH}}       ;"reference return 3rd axis"
                 AND.NOT   REFSTP_{{PATH}}       ;"stop reference return"
                 AND.NOT   inctr3_{{PATH}}       ;"increment counter 3rd axis"
                 SET       D_REF3_{{PATH}}       ;"3rd axis ref without dog"

                 RD        inctr3_{{PATH}}       ;"increment counter 3rd axis"
                 OR.NOT    R_REF3_{{PATH}}       ;"reference return 3rd axis"
                 OR        REFSTP_{{PATH}}       ;"stop reference return"
                 RST       D_REF3_{{PATH}}       ;"3rd axis ref without dog"

                 RD        M_REFJ_{{PATH}}       ;"jog mode during ref return"
                 AND       D_REF3_{{PATH}}       ;"3rd axis ref without dog"
                 AND.NOT   inctr3_{{PATH}}       ;"increment counter 3rd axis"
                 RD.STK    M_REF_{{PATH}}        ;"mode REF selected"
                 AND.NOT   M_REFJ_{{PATH}}       ;"jog mode during ref return"
                 AND       inctr3_{{PATH}}       ;"increment counter 3rd axis"
                 OR.STK    
                 AND       R_REF3_{{PATH}}       ;"reference return 3rd axis"
                 AND       D_JOGMV_{{PATH}}      ;"delay jog move"
   #if ref_dir_3rd_axis_{{PATH}} == 5
                 WRT       aref_3p_{{PATH}}      ;"ref: move 3rd axis +dir."
   #endif
   #if ref_dir_3rd_axis_{{PATH}} == 6
                 WRT       aref_3m_{{PATH}}      ;"ref: move 3rd axis -dir."
   #endif

#else
!! ---------------------------------------------------------------------------
!! {{PATH}}: in case of using the reference switch for the 3rd axis,
!! replace the contact 500ms with the real input signal Xxxx.y
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: in case of using the reference switch for the 3rd axis,
replace the contact 500ms with the real input signal Xxxx.y
*)
   #endif
                 RD        500MS                ;"500 ms"
                 WRT       m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"

!! ---------------------------------------------------------------------------
!! counter to specify the lenght of the deceleration dog for the
!! reference position return of the 3rd axis
!! ---------------------------------------------------------------------------
   #if net_comment == 1
      #if ref_dir_3rd_axis_{{PATH}} == 1 or ref_dir_3rd_axis_{{PATH}} == 2
(*
{{PATH}}: counter to specify the lenght of the deceleration dog for
the reference position return of the 3rd axis (contact: NC)
*)
      #endif
      #if ref_dir_3rd_axis_{{PATH}} == 3 or ref_dir_3rd_axis_{{PATH}} == 4
(*
{{PATH}}: counter to specify the lenght of the deceleration dog for
the reference position return of the 3rd axis (contact: NO)
*)
      #endif
   #endif
                 RD        LOG1                 ;"logic one"
                 RD.STK.NOT LOG1                ;"logic one"
   #if ref_dir_3rd_axis_{{PATH}} == 1 or ref_dir_3rd_axis_{{PATH}} == 2
                 RD.STK    m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
   #endif
   #if ref_dir_3rd_axis_{{PATH}} == 3 or ref_dir_3rd_axis_{{PATH}} == 4
                 RD.STK.NOT m_iref3_{{PATH}}     ;"mem ref. switch 3rd axis"
   #endif
                 RD.STK    CLOCK                ;"cycle clock pulse"
                 AND       MV3_{{PATH}}          ;"3rd axis moving signal"
                 CTRC
                           psdog3_{{PATH}}       ;"preset soft. dog 3rd axis"
                           csdog3_{{PATH}}       ;"counter soft. dog 3rd axis"
                 WRT       inctr3_{{PATH}}       ;"increment counter 3rd axis"

                 RD        inctr3_{{PATH}}       ;"increment counter 3rd axis"
                 RD.STK    esdog3_{{PATH}}       ;"end soft dec. dog 3rd axis"
   #if ref_dir_3rd_axis_{{PATH}} == 1 or ref_dir_3rd_axis_{{PATH}} == 2 
                 AND.NOT   m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
   #endif
   #if ref_dir_3rd_axis_{{PATH}} == 3 or ref_dir_3rd_axis_{{PATH}} == 4
                 AND       m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
   #endif
                 OR.STK
                 WRT       esdog3_{{PATH}}       ;"end soft dec. dog 3rd axis"

!! ---------------------------------------------------------------------------
!! 3rd axis reference position return
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: 3rd axis reference position return
*)
   #endif
                 RD        M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 OR        M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND       M_K3AX_{{PATH}}       ;"memory 3rd axis selected"
                 AND.NOT   AUTREF_{{PATH}}       ;"automatic reference return"
                 RD.STK    A_REF3_{{PATH}}       ;"auto ref. return 3rd axis"
                 OR        R_REF3_{{PATH}}       ;"reference return 3rd axis"
                 AND.NOT   ZP3_{{PATH}}          ;"ref position 3rd axis"
                 OR.STK
                 AND.NOT   REFSTP_{{PATH}}       ;"stop reference return"
                 AND.NOT   NOREF3_{{PATH}}       ;"no reference return 3rd axis"
                 WRT       R_REF3_{{PATH}}       ;"reference return 3rd axis"

!! ---------------------------------------------------------------------------
!! 3rd axis reference position return in plus direction
!! parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
!! 3003 bit 5 = 0 (DEC), 3006 bit 0 = 1 (GDC)
!! ---------------------------------------------------------------------------
   #if ref_dir_3rd_axis_{{PATH}} == 1 or ref_dir_3rd_axis_{{PATH}} == 3 
      #if net_comment == 1
         #if ref_dir_3rd_axis_{{PATH}} == 1
(*
{{PATH}}: 3rd axis reference position return in plus direction
(contact: NC, dog at negative axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
         #if ref_dir_3rd_axis_{{PATH}} == 3
(*
{{PATH}}: 3rd axis reference position return in plus direction
(contact: NO, dog at negative axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
      #endif
                 RD        R_REF3_{{PATH}}       ;"reference return 3rd axis"
      #if ref_dir_3rd_axis_{{PATH}} == 1
                 AND.NOT   m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
      #endif
      #if ref_dir_3rd_axis_{{PATH}} == 3
                 AND       m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
      #endif
                 AND.NOT   aref_3m_{{PATH}}      ;"ref: move 3rd axis -dir."
                 WRT       aref_3p_{{PATH}}      ;"ref: move 3rd axis +dir."

                 RD        R_REF3_{{PATH}}       ;"reference return 3rd axis"
                 AND.NOT   aref_3p_{{PATH}}      ;"ref: move 3rd axis +dir."
                 WRT       aref_3m_{{PATH}}      ;"ref: move 3rd axis -dir."
      #if ref_dir_3rd_axis_{{PATH}} == 1
                 RD        m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
      #endif
      #if ref_dir_3rd_axis_{{PATH}} == 3
                 RD.NOT    m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
      #endif
                 RD.STK    esdog3_{{PATH}}       ;"end soft dec. dog 3rd axis"
                 AND       MVD3_{{PATH}}         ;"3rd axis moving dir. signal"
                 OR.STK
                 OR        aref_3p_{{PATH}}      ;"ref: move 3rd axis +dir."
                 WRT       *DEC3_{{PATH}}        ;"deceleration signal 3rd axis"
   #endif

!! ---------------------------------------------------------------------------
!! 3rd axis reference position return in minus direction
!! parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
!! 3003 bit 5 = 0 (DEC), 3006 bit 0 = 1 (GDC)
!! ---------------------------------------------------------------------------
   #if ref_dir_3rd_axis_{{PATH}} == 2 or ref_dir_3rd_axis_{{PATH}} == 4
      #if net_comment == 1
         #if ref_dir_3rd_axis_{{PATH}} == 2
(*
{{PATH}}: 3rd axis reference position return in minus direction
(contact: NC, dog at positive axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
         #if ref_dir_3rd_axis_{{PATH}} == 4
(*
{{PATH}}: 3rd axis reference position return in minus direction
(contact: NO, dog at positive axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
      #endif
                 RD        R_REF3_{{PATH}}       ;"reference return 3rd axis"
      #if ref_dir_3rd_axis_{{PATH}} == 2
                 AND.NOT   m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
      #endif
      #if ref_dir_3rd_axis_{{PATH}} == 4
                 AND       m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
      #endif
                 AND.NOT   aref_3p_{{PATH}}      ;"ref: move 3rd axis +dir."
                 WRT       aref_3m_{{PATH}}      ;"ref: move 3rd axis -dir."

                 RD        R_REF3_{{PATH}}       ;"reference return 3rd axis"
                 AND.NOT   aref_3m_{{PATH}}      ;"ref: move 3rd axis -dir."
                 WRT       aref_3p_{{PATH}}      ;"ref: move 3rd axis +dir."
      #if ref_dir_3rd_axis_{{PATH}} == 2
                 RD        m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
      #endif
      #if ref_dir_3rd_axis_{{PATH}} == 4
                 RD.NOT    m_iref3_{{PATH}}      ;"mem ref. switch 3rd axis"
      #endif
                 RD.STK    esdog3_{{PATH}}       ;"end soft dec. dog 3rd axis"
                 AND.NOT   MVD3_{{PATH}}         ;"3rd axis moving dir. signal"
                 OR.STK
                 OR        aref_3m_{{PATH}}      ;"ref: move 3rd axis -dir."
                 WRT       *DEC3_{{PATH}}        ;"deceleration signal 3rd axis"
   #endif

#endif
