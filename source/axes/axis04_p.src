//:--------------------------------------------------------------------------:
//: File              : axis04__PATH__.src                                       :
//: Creation date     : 16.09.2013                                           :
//: Last modification : see table below                                      :
//: Author            : FANUC FA Switzerland / Peter Bloesch                 :
//: Description       : Management of 4th axis                               :
//:--------------------------------------------------------------------------:
//
//:--------------------------------------------------------------------------:
//:                     Modification                                         :
//:--------------------------------------------------------------------------:
//: Date   :Version: Sign  :Description                                      :
//:--------------------------------------------------------------------------:
//:05.11.18: 2.1.0 :Blp    :In case of brake control, the activation of the  :
//:        :       :       :*IT signal is accelerated when wp coordinate     :
//:        :       :       :preset is executed or hold to run button is      :
//:        :       :       :pressed.                                         :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:

// ===========================================================================
// Code
// ===========================================================================

// ---------------------------------------------------------------------------
// _PATH__: overtravel check
// if there are limit switches for the 4th axis,
// replace LOG1 with the real input signals Xxxx.y
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: overtravel check
if there are limit switches for the 4th axis,
replace LOG1 with the real input signals Xxxx.y
*)
#endif
                 RD        LOG1                 ;"logic one"
                 WRT       *+L4__PATH__         ;"overtravel + 4th axis"

                 RD        LOG1                 ;"logic one"
                 WRT       *-L4__PATH__         ;"overtravel - 4th axis"

// ---------------------------------------------------------------------------
// reference position return
// ---------------------------------------------------------------------------
#if ref_dir_4th_axis__PATH__ > 0 && ref_dir_1st_axis__PATH__ > -1
   #include "axis04ref__PATH__.src"       // reference position return 4th axis
#endif

// ---------------------------------------------------------------------------
// _PATH__: led 4th axis selected
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: led 4th axis selected
*)
#endif
                 RD        M_K4AX__PATH__       ;"memory 4th axis selected"
#if ref_dir_1st_axis__PATH__ > -1
                 AND.NOT   REFRUN__PATH__       ;"reference return active"
   #if ref_dir_4th_axis__PATH__ > 0
                 OR        R_REF4__PATH__       ;"reference return 4th axis"
   #endif
#endif
                 AND       M_MAN__PATH__        ;"manual mode active"
                 WRT       L_4AX__PATH__        ;"led 4th axis sel."

// ---------------------------------------------------------------------------
// _PATH__: jog feed
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: jog feed
*)
#endif
                 RD        M_K4AX__PATH__       ;"memory 4th axis selected"
                 AND       M_KPLUS__PATH__      ;"memory axis plus dir."
                 AND.NOT   M_REF__PATH__        ;"mode ref selected"
#if ref_dir_4th_axis__PATH__ > 0 && ref_dir_1st_axis__PATH__ > -1
                 OR        aref_4p__PATH__      ;"ref: move 4th axis +dir."
#endif
                 AND       AX_MEN__PATH__       ;"axis move enable"
#if spindle_support__PATH__ == 4 && spindle_sv_axis__PATH__ == 4 && spindle_support_P01 > -1
                 AND.NOT   EAX4__PATH__         ;"4th axis is pmc axis"
#endif
                 WRT       J4+__PATH__          ;"feed 4th axis + direction"
                 WRT       L_4AXP__PATH__       ;"led 4th axis + dir."

                 RD        M_K4AX__PATH__       ;"memory 4th axis selected"
                 AND       M_KMINUS__PATH__     ;"memory axis minus dir."
                 AND.NOT   M_REF__PATH__        ;"mode ref selected"
#if ref_dir_4th_axis__PATH__ > 0 && ref_dir_1st_axis__PATH__ > -1
                 OR        aref_4m__PATH__      ;"ref: move 4th axis -dir."
#endif
                 AND       AX_MEN__PATH__       ;"axis move enable"
#if spindle_support__PATH__ == 4 && spindle_sv_axis__PATH__ == 4 && spindle_support_P01 > -1
                 AND.NOT   EAX4__PATH__         ;"4th axis is pmc axis"
#endif
                 WRT       J4-__PATH__          ;"feed 4th axis - direction"
                 WRT       L_4AXM__PATH__       ;"led 4th axis - dir."

// ---------------------------------------------------------------------------
// _PATH__: workpiece coordinate system preset
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: workpiece coordinate system preset
*)
#endif
                 RD        REQWP4__PATH__       ;"req wp coord preset 4th axis"
                 AND       WPRST4__PATH__       ;"wp coord. preset 4th axis"
                 AND       WPSF4__PATH__        ;"wp coord. preset fin 4th axis"
                 OR        RST__PATH__          ;"resetting signal"
                 RST       REQWP4__PATH__       ;"req wp coord preset 4th axis"

                 RD        M104__PATH__         ;"wp coord preset 4th axis"
                 AND       WPRST4__PATH__       ;"wp coord preset 4th axis"
                 AND       WPSF4__PATH__        ;"wp coord preset fin 4th ax"
                 RST       M104__PATH__         ;"wp coord preset 4th axis"

                 RD        FE_RST__PATH__       ;"falling edge reset"
                 AND       wppsreq4__PATH__     ;"req wp coord. preset 4th ax"
                 RD.STK    FEPGMT__PATH__       ;"falling edge program test"
                 AND.NOT   PGMRUN__PATH__       ;"program running"
                 OR.STK    
                 OR        REQWP4__PATH__       ;"req wp coord preset 4th axis"
                 OR        M104__PATH__         ;"wp coord preset 4th axis"
                 RD.STK    WPRST4__PATH__       ;"wp coord. preset 4th axis"
                 AND.NOT   WPSF4__PATH__        ;"wp coord. preset fin 4th axis"
                 OR.STK    
                 AND.NOT   RST__PATH__          ;"resetting signal"
                 WRT       WPRST4__PATH__       ;"wp coord. preset 4th axis"

                 RD        FEPGMT__PATH__       ;"falling edge program test"
                 AND       PGMRUN__PATH__       ;"program running"
                 RD.STK    wppsreq4__PATH__     ;"req wp coord. preset 4th ax"
                 AND.NOT   FE_RST__PATH__       ;"falling edge reset"
                 OR.STK    
                 WRT       wppsreq4__PATH__     ;"req wp coord. preset 4th ax"

#if brake_4th_axis__PATH__ == 0
// ---------------------------------------------------------------------------
// _PATH__: axis interlock
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: axis interlock
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       POW_OK__PATH__       ;"power supply axis ok"
   #else
                 RD        POW_OK__PATH__       ;"power suply axis ok"
   #endif
                 AND.NOT   WPRST4__PATH__       ;"wp coord. preset 4th axis"
                 WRT       *IT4__PATH__         ;"interlock 4th axis"

// ---------------------------------------------------------------------------
// _PATH__: servo off
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: servo off
*)
   #endif
                 RD.NOT    POW_OK__PATH__       ;"power suply axis ok"
                 WRT       SVF4__PATH__         ;"servo off 4th axis"
#else
// ---------------------------------------------------------------------------
// _PATH__: brake control
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: brake control
*)
   #endif
                 RD        POW_OK__PATH__       ;"power supply axis ok"
                 AND       SA4__PATH__          ;"4th axis servo ready"
                 WRT       relb_4th__PATH__     ;"release brake 4th axis"

// ---------------------------------------------------------------------------
// _PATH__: delay brake released
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: delay brake released
*)
   #endif
                 RD        relb_4th__PATH__     ;"release brake 4th axis"
                 TMRB      
                           0
                           96
                 WRT       brel_4th__PATH__     ;"brake 4th axis released"

   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       brel_4th__PATH__     ;"brake 4th axis released"
   #else
                 RD        brel_4th__PATH__     ;"brake 4th axis released"
   #endif
                 AND.NOT   WPRST4__PATH__       ;"wp coord preset 4th axis"
                 WRT       *IT4__PATH__         ;"interlock 4th axis"

// ---------------------------------------------------------------------------
// _PATH__: delay brake clamped
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: delay brake clamped
*)
   #endif
                 RD.NOT    relb_4th__PATH__     ;"release brake 4th axis"
                 TMRB      
                           0
                           48
                 WRT       4th_soff__PATH__     ;"4th axis servo off"

// ---------------------------------------------------------------------------
// _PATH__: servo off
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: servo off
*)
   #endif
                 RD.NOT    FICYCL               ;"first cycle finished"
                 OR        1st_svf4__PATH__     ;"1st servo off 4th axis"
                 AND.NOT   POW_OK__PATH__       ;"power supply axis ok"
                 WRT       1st_svf4__PATH__     ;"1st servo off 4th axis"

                 RD        1st_svf4__PATH__     ;"1st servo off 4th axis"
                 OR        4th_soff__PATH__     ;"4th axis servo off"
                 WRT       SVF4__PATH__         ;"servo off 4th axis"
#endif
// ---------------------------------------------------------------------------
// _PATH__: external deceleration
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: external deceleration
*)
#endif
                 RD.NOT    SETUP__PATH__        ;"set-up active"
                 WRT       *+ED4__PATH__        ;"external dec. + 4th axis"
                 WRT       *-ED4__PATH__        ;"external dec. - 4th axis"

#if dcs_support == 1
// ---------------------------------------------------------------------------
// _PATH__: DCS MCC off signal
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS MCC off signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        *MCFV4__PATH__       ;"MCC off signal 4th axis"
   #endif
                 WRT       M*MCFV4__PATH__      ;"4th axis MCC off signal"

// ---------------------------------------------------------------------------
// _PATH__: DCS monitoring result signal
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS monitoring result signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        RSV4__PATH__         ;"monitoring result 4th axis"
   #endif
                 WRT       MRSV4__PATH__        ;"4th axis monit. result"

// ---------------------------------------------------------------------------
// _PATH__: position information effect signal
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
DCS position information effect signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        POSE4__PATH__        ;"pos. inf. effect 4th axis"
   #endif
                 WRT       MPOSE4__PATH__       ;"4th axis pos. inf. effect"

// ---------------------------------------------------------------------------
// _PATH__: DCS safety check activation
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS safety check activation
*)
   #endif
                 RD        *SGOPN               ;"guard state doors closed"
                 WRT       *VLDV4__PATH__       ;"safety check req. 4th ax."

// ---------------------------------------------------------------------------
// _PATH__: DCS safety zero speed monitoring
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS safety zero speed monitoring
*)
   #endif
                 RD.NOT    DCS_S02              ;"DCS speed set 2"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 WRT       SVZR4__PATH__        ;"zero mon. req. 4th axis"

// ---------------------------------------------------------------------------
// _PATH__: DCS selection of safety speed set 2
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS selection of safety speed set 2
*)
   #endif
                 RD        DCS_S02              ;"DCS speed set 2"
                 WRT       SVA4__PATH__         ;"saf. sel. sig. A 4th axis"
#endif

#if brake_4th_axis__PATH__ == 1
// ---------------------------------------------------------------------------
// _PATH__: management of the brake output
// replace T_BIT000 with the real brake output Yxxx.y
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: management of the brake output
replace T_BIT000 with the real brake output Yxxx.y
*)
   #endif

   #if dcs_support == 0
                 RD        relb_4th__PATH__     ;"release brake 4th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #else
                 RD        relb_4th__PATH__     ;"release brake 4th axis"
                 AND       *BRK4__PATH__        ;"Safety Brake sig. 4th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #endif
#endif
