!!:--------------------------------------------------------------------------:
!!: File              : axis04_{{PATH}}.src                                       :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Management of 4th axis                               :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:05.11.18: 2.1.0 :Blp    :In case of brake control, the activation of the  :
!!:        :       :       :*IT signal is accelerated when wp coordinate     :
!!:        :       :       :preset is executed or hold to run button is      :
!!:        :       :       :pressed.                                         :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

!! ---------------------------------------------------------------------------
!! {{PATH}}: overtravel check
!! if there are limit switches for the 4th axis,
!! replace LOG1 with the real input signals Xxxx.y
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: overtravel check
if there are limit switches for the 4th axis,
replace LOG1 with the real input signals Xxxx.y
*)
#endif
                 RD        LOG1                 ;"logic one"
                 WRT       *+L4_{{PATH}}         ;"overtravel + 4th axis"

                 RD        LOG1                 ;"logic one"
                 WRT       *-L4_{{PATH}}         ;"overtravel - 4th axis"

!! ---------------------------------------------------------------------------
!! reference position return
!! ---------------------------------------------------------------------------
#if ref_dir_4th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
   #include "axis04ref_{{PATH}}.src"       !! reference position return 4th axis
#endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: led 4th axis selected
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: led 4th axis selected
*)
#endif
                 RD        M_K4AX_{{PATH}}       ;"memory 4th axis selected"
#if ref_dir_1st_axis_{{PATH}} > -1
                 AND.NOT   REFRUN_{{PATH}}       ;"reference return active"
   #if ref_dir_4th_axis_{{PATH}} > 0
                 OR        R_REF4_{{PATH}}       ;"reference return 4th axis"
   #endif
#endif
                 AND       M_MAN_{{PATH}}        ;"manual mode active"
                 WRT       L_4AX_{{PATH}}        ;"led 4th axis sel."

!! ---------------------------------------------------------------------------
!! {{PATH}}: jog feed
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: jog feed
*)
#endif
                 RD        M_K4AX_{{PATH}}       ;"memory 4th axis selected"
                 AND       M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_4th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_4p_{{PATH}}      ;"ref: move 4th axis +dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 4 and spindle_support_P01 > -1
                 AND.NOT   EAX4_{{PATH}}         ;"4th axis is pmc axis"
#endif
                 WRT       J4+_{{PATH}}          ;"feed 4th axis + direction"
                 WRT       L_4AXP_{{PATH}}       ;"led 4th axis + dir."

                 RD        M_K4AX_{{PATH}}       ;"memory 4th axis selected"
                 AND       M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_4th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_4m_{{PATH}}      ;"ref: move 4th axis -dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 4 and spindle_support_P01 > -1
                 AND.NOT   EAX4_{{PATH}}         ;"4th axis is pmc axis"
#endif
                 WRT       J4-_{{PATH}}          ;"feed 4th axis - direction"
                 WRT       L_4AXM_{{PATH}}       ;"led 4th axis - dir."

!! ---------------------------------------------------------------------------
!! {{PATH}}: workpiece coordinate system preset
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: workpiece coordinate system preset
*)
#endif
                 RD        REQWP4_{{PATH}}       ;"req wp coord preset 4th axis"
                 AND       WPRST4_{{PATH}}       ;"wp coord. preset 4th axis"
                 AND       WPSF4_{{PATH}}        ;"wp coord. preset fin 4th axis"
                 OR        RST_{{PATH}}          ;"resetting signal"
                 RST       REQWP4_{{PATH}}       ;"req wp coord preset 4th axis"

                 RD        M104_{{PATH}}         ;"wp coord preset 4th axis"
                 AND       WPRST4_{{PATH}}       ;"wp coord preset 4th axis"
                 AND       WPSF4_{{PATH}}        ;"wp coord preset fin 4th ax"
                 RST       M104_{{PATH}}         ;"wp coord preset 4th axis"

                 RD        FE_RST_{{PATH}}       ;"falling edge reset"
                 AND       wppsreq4_{{PATH}}     ;"req wp coord. preset 4th ax"
                 RD.STK    FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND.NOT   PGMRUN_{{PATH}}       ;"program running"
                 OR.STK    
                 OR        REQWP4_{{PATH}}       ;"req wp coord preset 4th axis"
                 OR        M104_{{PATH}}         ;"wp coord preset 4th axis"
                 RD.STK    WPRST4_{{PATH}}       ;"wp coord. preset 4th axis"
                 AND.NOT   WPSF4_{{PATH}}        ;"wp coord. preset fin 4th axis"
                 OR.STK    
                 AND.NOT   RST_{{PATH}}          ;"resetting signal"
                 WRT       WPRST4_{{PATH}}       ;"wp coord. preset 4th axis"

                 RD        FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND       PGMRUN_{{PATH}}       ;"program running"
                 RD.STK    wppsreq4_{{PATH}}     ;"req wp coord. preset 4th ax"
                 AND.NOT   FE_RST_{{PATH}}       ;"falling edge reset"
                 OR.STK    
                 WRT       wppsreq4_{{PATH}}     ;"req wp coord. preset 4th ax"

#if brake_4th_axis_{{PATH}} == 0
!! ---------------------------------------------------------------------------
!! {{PATH}}: axis interlock
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: axis interlock
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       POW_OK_{{PATH}}       ;"power supply axis ok"
   #else
                 RD        POW_OK_{{PATH}}       ;"power suply axis ok"
   #endif
                 AND.NOT   WPRST4_{{PATH}}       ;"wp coord. preset 4th axis"
                 WRT       *IT4_{{PATH}}         ;"interlock 4th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    POW_OK_{{PATH}}       ;"power suply axis ok"
                 WRT       SVF4_{{PATH}}         ;"servo off 4th axis"
#else
!! ---------------------------------------------------------------------------
!! {{PATH}}: brake control
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: brake control
*)
   #endif
                 RD        POW_OK_{{PATH}}       ;"power supply axis ok"
                 AND       SA4_{{PATH}}          ;"4th axis servo ready"
                 WRT       relb_4th_{{PATH}}     ;"release brake 4th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake released
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake released
*)
   #endif
                 RD        relb_4th_{{PATH}}     ;"release brake 4th axis"
                 TMRB      
                           0
                           96
                 WRT       brel_4th_{{PATH}}     ;"brake 4th axis released"

   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       brel_4th_{{PATH}}     ;"brake 4th axis released"
   #else
                 RD        brel_4th_{{PATH}}     ;"brake 4th axis released"
   #endif
                 AND.NOT   WPRST4_{{PATH}}       ;"wp coord preset 4th axis"
                 WRT       *IT4_{{PATH}}         ;"interlock 4th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake clamped
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake clamped
*)
   #endif
                 RD.NOT    relb_4th_{{PATH}}     ;"release brake 4th axis"
                 TMRB      
                           0
                           48
                 WRT       4th_soff_{{PATH}}     ;"4th axis servo off"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    FICYCL               ;"first cycle finished"
                 OR        1st_svf4_{{PATH}}     ;"1st servo off 4th axis"
                 AND.NOT   POW_OK_{{PATH}}       ;"power supply axis ok"
                 WRT       1st_svf4_{{PATH}}     ;"1st servo off 4th axis"

                 RD        1st_svf4_{{PATH}}     ;"1st servo off 4th axis"
                 OR        4th_soff_{{PATH}}     ;"4th axis servo off"
                 WRT       SVF4_{{PATH}}         ;"servo off 4th axis"
#endif
!! ---------------------------------------------------------------------------
!! {{PATH}}: external deceleration
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: external deceleration
*)
#endif
                 RD.NOT    SETUP_{{PATH}}        ;"set-up active"
                 WRT       *+ED4_{{PATH}}        ;"external dec. + 4th axis"
                 WRT       *-ED4_{{PATH}}        ;"external dec. - 4th axis"

#if dcs_support == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS MCC off signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS MCC off signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        *MCFV4_{{PATH}}       ;"MCC off signal 4th axis"
   #endif
                 WRT       M*MCFV4_{{PATH}}      ;"4th axis MCC off signal"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS monitoring result signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS monitoring result signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        RSV4_{{PATH}}         ;"monitoring result 4th axis"
   #endif
                 WRT       MRSV4_{{PATH}}        ;"4th axis monit. result"

!! ---------------------------------------------------------------------------
!! {{PATH}}: position information effect signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
DCS position information effect signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        POSE4_{{PATH}}        ;"pos. inf. effect 4th axis"
   #endif
                 WRT       MPOSE4_{{PATH}}       ;"4th axis pos. inf. effect"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety check activation
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety check activation
*)
   #endif
                 RD        *SGOPN               ;"guard state doors closed"
                 WRT       *VLDV4_{{PATH}}       ;"safety check req. 4th ax."

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety zero speed monitoring
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety zero speed monitoring
*)
   #endif
                 RD.NOT    DCS_S02              ;"DCS speed set 2"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 WRT       SVZR4_{{PATH}}        ;"zero mon. req. 4th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS selection of safety speed set 2
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS selection of safety speed set 2
*)
   #endif
                 RD        DCS_S02              ;"DCS speed set 2"
                 WRT       SVA4_{{PATH}}         ;"saf. sel. sig. A 4th axis"
#endif

#if brake_4th_axis_{{PATH}} == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: management of the brake output
!! replace T_BIT000 with the real brake output Yxxx.y
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: management of the brake output
replace T_BIT000 with the real brake output Yxxx.y
*)
   #endif

   #if dcs_support == 0
                 RD        relb_4th_{{PATH}}     ;"release brake 4th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #else
                 RD        relb_4th_{{PATH}}     ;"release brake 4th axis"
                 AND       *BRK4_{{PATH}}        ;"Safety Brake sig. 4th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #endif
#endif
