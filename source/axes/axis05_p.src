!!:--------------------------------------------------------------------------:
!!: File              : axis05_{{PATH}}.src                                       :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Management of 5th axis                               :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:05.11.18: 2.1.0 :Blp    :In case of brake control, the activation of the  :
!!:        :       :       :*IT signal is accelerated when wp coordinate     :
!!:        :       :       :preset is executed or hold to run button is      :
!!:        :       :       :pressed.                                         :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

!! ---------------------------------------------------------------------------
!! {{PATH}}: overtravel check
!! if there are limit switches for the 5th axis,
!! replace LOG1 with the real input signals Xxxx.y
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: overtravel check
if there are limit switches for the 5th axis,
replace LOG1 with the real input signals Xxxx.y
*)
#endif
                 RD        LOG1                 ;"logic one"
                 WRT       *+L5_{{PATH}}         ;"overtravel + 5th axis"

                 RD        LOG1                 ;"logic one"
                 WRT       *-L5_{{PATH}}         ;"overtravel - 5th axis"

!! ---------------------------------------------------------------------------
!! reference position return
!! ---------------------------------------------------------------------------
#if ref_dir_5th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
   #include "axis05ref_{{PATH}}.src"       !! reference position return 5th axis
#endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: led 5th axis selected
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: led 5th axis selected
*)
#endif
                 RD        M_K5AX_{{PATH}}       ;"memory 5th axis selected"
#if ref_dir_1st_axis_{{PATH}} > -1
                 AND.NOT   REFRUN_{{PATH}}       ;"reference return active"
   #if ref_dir_5th_axis_{{PATH}} > 0
                 OR        R_REF5_{{PATH}}       ;"reference return 5th axis"
   #endif
#endif
                 AND       M_MAN_{{PATH}}        ;"manual mode active"
                 WRT       L_5AX_{{PATH}}        ;"led 5th axis sel."

!! ---------------------------------------------------------------------------
!! {{PATH}}: jog feed
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: jog feed
*)
#endif
                 RD        M_K5AX_{{PATH}}       ;"memory 5th axis selected"
                 AND       M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_5th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_5p_{{PATH}}      ;"ref: move 5th axis +dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 5 and spindle_support_P01 > -1
                 AND.NOT   EAX5_{{PATH}}         ;"5th axis is pmc axis"
#endif
                 WRT       J5+_{{PATH}}          ;"feed 5th axis + direction"
                 WRT       L_5AXP_{{PATH}}       ;"led 5th axis + dir."

                 RD        M_K5AX_{{PATH}}       ;"memory 5th axis selected"
                 AND       M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_5th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_5m_{{PATH}}      ;"ref: move 5th axis -dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 5 and spindle_support_P01 > -1
                 AND.NOT   EAX5_{{PATH}}         ;"5th axis is pmc axis"
#endif
                 WRT       J5-_{{PATH}}          ;"feed 5th axis - direction"
                 WRT       L_5AXM_{{PATH}}       ;"led 5th axis - dir."

!! ---------------------------------------------------------------------------
!! {{PATH}}: workpiece coordinate system preset
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: workpiece coordinate system preset
*)
#endif
                 RD        REQWP5_{{PATH}}       ;"req wp coord preset 5th axis"
                 AND       WPRST5_{{PATH}}       ;"wp coord. preset 5th axis"
                 AND       WPSF5_{{PATH}}        ;"wp coord. preset fin 5th axis"
                 OR        RST_{{PATH}}          ;"resetting signal"
                 RST       REQWP5_{{PATH}}       ;"req wp coord preset 5th axis"

                 RD        M105_{{PATH}}         ;"wp coord preset 5th axis"
                 AND       WPRST5_{{PATH}}       ;"wp coord preset 5th axis"
                 AND       WPSF5_{{PATH}}        ;"wp coord preset fin 5th ax"
                 RST       M105_{{PATH}}         ;"wp coord preset 5th axis"

                 RD        FE_RST_{{PATH}}       ;"falling edge reset"
                 AND       wppsreq5_{{PATH}}     ;"req wp coord. preset 5th ax"
                 RD.STK    FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND.NOT   PGMRUN_{{PATH}}       ;"program running"
                 OR.STK    
                 OR        REQWP5_{{PATH}}       ;"req wp coord preset 5th axis"
                 OR        M105_{{PATH}}         ;"wp coord preset 5th axis"
                 RD.STK    WPRST5_{{PATH}}       ;"wp coord. preset 5th axis"
                 AND.NOT   WPSF5_{{PATH}}        ;"wp coord. preset fin 5th axis"
                 OR.STK    
                 AND.NOT   RST_{{PATH}}          ;"resetting signal"
                 WRT       WPRST5_{{PATH}}       ;"wp coord. preset 5th axis"

                 RD        FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND       PGMRUN_{{PATH}}       ;"program running"
                 RD.STK    wppsreq5_{{PATH}}     ;"req wp coord. preset 5th ax"
                 AND.NOT   FE_RST_{{PATH}}       ;"falling edge reset"
                 OR.STK    
                 WRT       wppsreq5_{{PATH}}     ;"req wp coord. preset 5th ax"

#if brake_5th_axis_{{PATH}} == 0
!! ---------------------------------------------------------------------------
!! {{PATH}}: axis interlock
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: axis interlock
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       POW_OK_{{PATH}}       ;"power supply axis ok"
   #else
                 RD        POW_OK_{{PATH}}       ;"power suply axis ok"
   #endif
                 AND.NOT   WPRST5_{{PATH}}       ;"wp coord. preset 5th axis"
                 WRT       *IT5_{{PATH}}         ;"interlock 5th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    POW_OK_{{PATH}}       ;"power suply axis ok"
                 WRT       SVF5_{{PATH}}         ;"servo off 5th axis"
#else
!! ---------------------------------------------------------------------------
!! {{PATH}}: brake control
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: brake control
*)
   #endif
                 RD        POW_OK_{{PATH}}       ;"power supply axis ok"
                 AND       SA5_{{PATH}}          ;"5th axis servo ready"
                 WRT       relb_5th_{{PATH}}     ;"release brake 5th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake released
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake released
*)
   #endif
                 RD        relb_5th_{{PATH}}     ;"release brake 5th axis"
                 TMRB      
                           0
                           96
                 WRT       brel_5th_{{PATH}}     ;"brake 5th axis released"

   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       brel_5th_{{PATH}}     ;"brake 5th axis released"
   #else
                 RD        brel_5th_{{PATH}}     ;"brake 5th axis released"
   #endif
                 AND.NOT   WPRST5_{{PATH}}       ;"wp coord preset 5th axis"
                 WRT       *IT5_{{PATH}}         ;"interlock 5th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake clamped
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake clamped
*)
   #endif
                 RD.NOT    relb_5th_{{PATH}}     ;"release brake 5th axis"
                 TMRB      
                           0
                           48
                 WRT       5th_soff_{{PATH}}     ;"5th axis servo off"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    FICYCL               ;"first cycle finished"
                 OR        1st_svf5_{{PATH}}     ;"1st servo off 5th axis"
                 AND.NOT   POW_OK_{{PATH}}       ;"power supply axis ok"
                 WRT       1st_svf5_{{PATH}}     ;"1st servo off 5th axis"

                 RD        1st_svf5_{{PATH}}     ;"1st servo off 5th axis"
                 OR        5th_soff_{{PATH}}     ;"5th axis servo off"
                 WRT       SVF5_{{PATH}}         ;"servo off 5th axis"
#endif
!! ---------------------------------------------------------------------------
!! {{PATH}}: external deceleration
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: external deceleration
*)
#endif
                 RD.NOT    SETUP_{{PATH}}        ;"set-up active"
                 WRT       *+ED5_{{PATH}}        ;"external dec. + 5th axis"
                 WRT       *-ED5_{{PATH}}        ;"external dec. - 5th axis"

#if dcs_support == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS MCC off signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS MCC off signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        *MCFV5_{{PATH}}       ;"MCC off signal 5th axis"
   #endif
                 WRT       M*MCFV5_{{PATH}}      ;"5th axis MCC off signal"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS monitoring result signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS monitoring result signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        RSV5_{{PATH}}         ;"monitoring result 5th axis"
   #endif
                 WRT       MRSV5_{{PATH}}        ;"5th axis monit. result"

!! ---------------------------------------------------------------------------
!! {{PATH}}: position information effect signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
DCS position information effect signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        POSE5_{{PATH}}        ;"pos. inf. effect 5th axis"
   #endif
                 WRT       MPOSE5_{{PATH}}       ;"5th axis pos. inf. effect"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety check activation
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety check activation
*)
   #endif
                 RD        *SGOPN               ;"guard state doors closed"
                 WRT       *VLDV5_{{PATH}}       ;"safety check req. 5th ax."

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety zero speed monitoring
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety zero speed monitoring
*)
   #endif
                 RD.NOT    DCS_S02              ;"DCS speed set 2"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 WRT       SVZR5_{{PATH}}        ;"zero mon. req. 5th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS selection of safety speed set 2
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS selection of safety speed set 2
*)
   #endif
                 RD        DCS_S02              ;"DCS speed set 2"
                 WRT       SVA5_{{PATH}}         ;"saf. sel. sig. A 5th axis"
#endif

#if brake_5th_axis_{{PATH}} == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: management of the brake output
!! replace T_BIT000 with the real brake output Yxxx.y
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: management of the brake output
replace T_BIT000 with the real brake output Yxxx.y
*)
   #endif

   #if dcs_support == 0
                 RD        relb_5th_{{PATH}}     ;"release brake 5th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #else
                 RD        relb_5th_{{PATH}}     ;"release brake 5th axis"
                 AND       *BRK5_{{PATH}}        ;"Safety Brake sig. 5th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #endif
#endif

