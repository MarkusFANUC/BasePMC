!!:--------------------------------------------------------------------------:
!!: File              : axis06_{{PATH}}.src                                       :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Management of 6th axis                               :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:05.11.18: 2.1.0 :Blp    :In case of brake control, the activation of the  :
!!:        :       :       :*IT signal is accelerated when wp coordinate     :
!!:        :       :       :preset is executed or hold to run button is      :
!!:        :       :       :pressed.                                         :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

!! ---------------------------------------------------------------------------
!! {{PATH}}: overtravel check
!! if there are limit switches for the 6th axis,
!! replace LOG1 with the real input signals Xxxx.y
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: overtravel check
if there are limit switches for the 6th axis,
replace LOG1 with the real input signals Xxxx.y
*)
#endif
                 RD        LOG1                 ;"logic one"
                 WRT       *+L6_{{PATH}}         ;"overtravel + 6th axis"

                 RD        LOG1                 ;"logic one"
                 WRT       *-L6_{{PATH}}         ;"overtravel - 6th axis"

!! ---------------------------------------------------------------------------
!! reference position return
!! ---------------------------------------------------------------------------
#if ref_dir_6th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
   #include "axis06ref_{{PATH}}.src"       !! reference position return 6th axis
#endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: led 6th axis selected
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: led 6th axis selected
*)
#endif
                 RD        M_K6AX_{{PATH}}       ;"memory 6th axis selected"
#if ref_dir_1st_axis_{{PATH}} > -1
                 AND.NOT   REFRUN_{{PATH}}       ;"reference return active"
   #if ref_dir_6th_axis_{{PATH}} > 0
                 OR        R_REF6_{{PATH}}       ;"reference return 6th axis"
   #endif
#endif
                 AND       M_MAN_{{PATH}}        ;"manual mode active"
                 WRT       L_6AX_{{PATH}}        ;"led 6th axis sel."

!! ---------------------------------------------------------------------------
!! {{PATH}}: jog feed
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: jog feed
*)
#endif
                 RD        M_K6AX_{{PATH}}       ;"memory 6th axis selected"
                 AND       M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_6th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_6p_{{PATH}}      ;"ref: move 6th axis +dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 6 and spindle_support_P01 > -1
                 AND.NOT   EAX6_{{PATH}}         ;"6th axis is pmc axis"
#endif
                 WRT       J6+_{{PATH}}          ;"feed 6th axis + direction"
                 WRT       L_6AXP_{{PATH}}       ;"led 6th axis + dir."

                 RD        M_K6AX_{{PATH}}       ;"memory 6th axis selected"
                 AND       M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_6th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_6m_{{PATH}}      ;"ref: move 6th axis -dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 6 and spindle_support_P01 > -1
                 AND.NOT   EAX6_{{PATH}}         ;"6th axis is pmc axis"
#endif
                 WRT       J6-_{{PATH}}          ;"feed 6th axis - direction"
                 WRT       L_6AXM_{{PATH}}       ;"led 6th axis - dir."

!! ---------------------------------------------------------------------------
!! {{PATH}}: workpiece coordinate system preset
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: workpiece coordinate system preset
*)
#endif
                 RD        REQWP6_{{PATH}}       ;"req wp coord preset 6th axis"
                 AND       WPRST6_{{PATH}}       ;"wp coord. preset 6th axis"
                 AND       WPSF6_{{PATH}}        ;"wp coord. preset fin 6th axis"
                 OR        RST_{{PATH}}          ;"resetting signal"
                 RST       REQWP6_{{PATH}}       ;"req wp coord preset 6th axis"

                 RD        M106_{{PATH}}         ;"wp coord preset 6th axis"
                 AND       WPRST6_{{PATH}}       ;"wp coord preset 6th axis"
                 AND       WPSF6_{{PATH}}        ;"wp coord preset fin 6th ax"
                 RST       M106_{{PATH}}         ;"wp coord preset 6th axis"

                 RD        FE_RST_{{PATH}}       ;"falling edge reset"
                 AND       wppsreq6_{{PATH}}     ;"req wp coord. preset 6th ax"
                 RD.STK    FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND.NOT   PGMRUN_{{PATH}}       ;"program running"
                 OR.STK    
                 OR        REQWP6_{{PATH}}       ;"req wp coord preset 6th axis"
                 OR        M106_{{PATH}}         ;"wp coord preset 6th axis"
                 RD.STK    WPRST6_{{PATH}}       ;"wp coord. preset 6th axis"
                 AND.NOT   WPSF6_{{PATH}}        ;"wp coord. preset fin 6th axis"
                 OR.STK    
                 AND.NOT   RST_{{PATH}}          ;"resetting signal"
                 WRT       WPRST6_{{PATH}}       ;"wp coord. preset 6th axis"

                 RD        FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND       PGMRUN_{{PATH}}       ;"program running"
                 RD.STK    wppsreq6_{{PATH}}     ;"req wp coord. preset 6th ax"
                 AND.NOT   FE_RST_{{PATH}}       ;"falling edge reset"
                 OR.STK    
                 WRT       wppsreq6_{{PATH}}     ;"req wp coord. preset 6th ax"

#if brake_6th_axis_{{PATH}} == 0
!! ---------------------------------------------------------------------------
!! {{PATH}}: axis interlock
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: axis interlock
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       POW_OK_{{PATH}}       ;"power supply axis ok"
   #else
                 RD        POW_OK_{{PATH}}       ;"power suply axis ok"
   #endif
                 AND.NOT   WPRST6_{{PATH}}       ;"wp coord. preset 6th axis"
                 WRT       *IT6_{{PATH}}         ;"interlock 6th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    POW_OK_{{PATH}}       ;"power suply axis ok"
                 WRT       SVF6_{{PATH}}         ;"servo off 6th axis"
#else
!! ---------------------------------------------------------------------------
!! {{PATH}}: brake control
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: brake control
*)
   #endif
                 RD        POW_OK_{{PATH}}       ;"power supply axis ok"
                 AND       SA6_{{PATH}}          ;"6th axis servo ready"
                 WRT       relb_6th_{{PATH}}     ;"release brake 6th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake released
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake released
*)
   #endif
                 RD        relb_6th_{{PATH}}     ;"release brake 6th axis"
                 TMRB      
                           0
                           96
                 WRT       brel_6th_{{PATH}}     ;"brake 6th axis released"

   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       brel_6th_{{PATH}}     ;"brake 6th axis released"
   #else
                 RD        brel_6th_{{PATH}}     ;"brake 6th axis released"
   #endif
                 AND.NOT   WPRST6_{{PATH}}       ;"wp coord preset 6th axis"
                 WRT       *IT6_{{PATH}}         ;"interlock 6th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake clamped
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake clamped
*)
   #endif
                 RD.NOT    relb_6th_{{PATH}}     ;"release brake 6th axis"
                 TMRB      
                           0
                           48
                 WRT       6th_soff_{{PATH}}     ;"6th axis servo off"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    FICYCL               ;"first cycle finished"
                 OR        1st_svf6_{{PATH}}     ;"1st servo off 6th axis"
                 AND.NOT   POW_OK_{{PATH}}       ;"power supply axis ok"
                 WRT       1st_svf6_{{PATH}}     ;"1st servo off 6th axis"

                 RD        1st_svf6_{{PATH}}     ;"1st servo off 6th axis"
                 OR        6th_soff_{{PATH}}     ;"6th axis servo off"
                 WRT       SVF6_{{PATH}}         ;"servo off 6th axis"
#endif
!! ---------------------------------------------------------------------------
!! {{PATH}}: external deceleration
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: external deceleration
*)
#endif
                 RD.NOT    SETUP_{{PATH}}        ;"set-up active"
                 WRT       *+ED6_{{PATH}}        ;"external dec. + 6th axis"
                 WRT       *-ED6_{{PATH}}        ;"external dec. - 6th axis"

#if dcs_support == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS MCC off signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS MCC off signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        *MCFV6_{{PATH}}       ;"MCC off signal 6th axis"
   #endif
                 WRT       M*MCFV6_{{PATH}}      ;"6th axis MCC off signal"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS monitoring result signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS monitoring result signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        RSV6_{{PATH}}         ;"monitoring result 6th axis"
   #endif
                 WRT       MRSV6_{{PATH}}        ;"6th axis monit. result"

!! ---------------------------------------------------------------------------
!! {{PATH}}: position information effect signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
DCS position information effect signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        POSE6_{{PATH}}        ;"pos. inf. effect 6th axis"
   #endif
                 WRT       MPOSE6_{{PATH}}       ;"6th axis pos. inf. effect"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety check activation
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety check activation
*)
   #endif
                 RD        *SGOPN               ;"guard state doors closed"
                 WRT       *VLDV6_{{PATH}}       ;"safety check req. 6th ax."

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety zero speed monitoring
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety zero speed monitoring
*)
   #endif
                 RD.NOT    DCS_S02              ;"DCS speed set 2"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 WRT       SVZR6_{{PATH}}        ;"zero mon. req. 6th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS selection of safety speed set 2
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS selection of safety speed set 2
*)
   #endif
                 RD        DCS_S02              ;"DCS speed set 2"
                 WRT       SVA6_{{PATH}}         ;"saf. sel. sig. A 6th axis"
#endif

#if brake_6th_axis_{{PATH}} == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: management of the brake output
!! replace T_BIT000 with the real brake output Yxxx.y
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: management of the brake output
replace T_BIT000 with the real brake output Yxxx.y
*)
   #endif

   #if dcs_support == 0
                 RD        relb_6th_{{PATH}}     ;"release brake 6th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #else
                 RD        relb_6th_{{PATH}}     ;"release brake 6th axis"
                 AND       *BRK6_{{PATH}}        ;"Safety Brake sig. 6th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #endif
#endif
