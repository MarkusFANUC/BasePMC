!!:--------------------------------------------------------------------------:
!!: File              : axis06ref_{{PATH}}.src                                    :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Reference position return 6th axis                   :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

#if ref_dir_6th_axis_{{PATH}} == 5 or ref_dir_6th_axis_{{PATH}} == 6

!! ---------------------------------------------------------------------------
!! 6th axis reference position return in plus direction without dog
!! parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 0 (ZMI)
!! ---------------------------------------------------------------------------
   #if ref_dir_6th_axis_{{PATH}} == 5
      #if net_comment == 1
(*
{{PATH}}: 6th axis reference position return in plus direction
without dog
parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 0 (ZMI)
*)
      #endif
   #endif

!! ---------------------------------------------------------------------------
!! 6th axis reference position return in minus direction without dog
!! parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 1 (ZMI)
!! ---------------------------------------------------------------------------
   #if ref_dir_6th_axis_{{PATH}} == 6
(*
{{PATH}}: 6th axis reference position return in minus direction
without dog
parameter setting: 1005 bit 1 = 1 (DLZx), 1006 bit 5 = 1 (ZMI)
*)
   #endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: timer to specify the time for one motor revolution
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: timer to specify the time for one motor revolution
*)
   #endif
                 RD        R_REF6_{{PATH}}       ;"reference return 6th axis"
                 AND       M_REFJ_{{PATH}}       ;"jog mode during ref return"
                 AND       MV6_{{PATH}}          ;"6th axis moving signal"
                 TMRC      
                           5
                           psdog6_{{PATH}}       ;"preset soft. dog 6th axis"
                           csdog6_{{PATH}}       ;"counter soft. dog 6th axis"
                 SET       inctr6_{{PATH}}       ;"increment counter 6th axis"

                 RD        REFSTP_{{PATH}}       ;"stop reference return"
                 OR        ZP6_{{PATH}}          ;"ref position 6th axis"
                 AND       inctr6_{{PATH}}       ;"increment counter 6th axis"
                 RST       inctr6_{{PATH}}       ;"increment counter 6th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: 6th axis reference position return
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: 6th axis reference position return
*)
   #endif
                 RD        M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 OR        M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND       M_K6AX_{{PATH}}       ;"memory 6th axis selected"
                 AND.NOT   AUTREF_{{PATH}}       ;"automatic refernce return"
                 RD.STK    A_REF6_{{PATH}}       ;"auto ref. return 6th axis"
                 OR        R_REF6_{{PATH}}       ;"reference return 6th axis"
                 AND.NOT   ZP6_{{PATH}}          ;"ref position 6th axis"
                 OR.STK    
                 AND.NOT   REFSTP_{{PATH}}       ;"stop reference return"
                 AND.NOT   NOREF6_{{PATH}}       ;"no reference return 6th axis"
                 WRT       R_REF6_{{PATH}}       ;"reference return 6th axis"

                 RD        R_REF6_{{PATH}}       ;"reference return 6th axis"
                 AND.NOT   REFSTP_{{PATH}}       ;"stop reference return"
                 AND.NOT   inctr6_{{PATH}}       ;"increment counter 6th axis"
                 SET       D_REF6_{{PATH}}       ;"6th axis ref without dog"

                 RD        inctr6_{{PATH}}       ;"increment counter 6th axis"
                 OR.NOT    R_REF6_{{PATH}}       ;"reference return 6th axis"
                 OR        REFSTP_{{PATH}}       ;"stop reference return"
                 RST       D_REF6_{{PATH}}       ;"6th axis ref without dog"

                 RD        M_REFJ_{{PATH}}       ;"jog mode during ref return"
                 AND       D_REF6_{{PATH}}       ;"6th axis ref without dog"
                 AND.NOT   inctr6_{{PATH}}       ;"increment counter 6th axis"
                 RD.STK    M_REF_{{PATH}}        ;"mode REF selected"
                 AND.NOT   M_REFJ_{{PATH}}       ;"jog mode during ref return"
                 AND       inctr6_{{PATH}}       ;"increment counter 6th axis"
                 OR.STK    
                 AND       R_REF6_{{PATH}}       ;"reference return 6th axis"
                 AND       D_JOGMV_{{PATH}}      ;"delay jog move"
   #if ref_dir_6th_axis_{{PATH}} == 5
                 WRT       aref_6p_{{PATH}}      ;"ref: move 6th axis +dir."
   #endif
   #if ref_dir_6th_axis_{{PATH}} == 6
                 WRT       aref_6m_{{PATH}}      ;"ref: move 6th axis -dir."
   #endif

#else
!! ---------------------------------------------------------------------------
!! {{PATH}}: in case of using the reference switch for the 6th axis,
!! replace the contact 500ms with the real input signal Xxxx.y
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: in case of using the reference switch for the 6th axis,
replace the contact 500ms with the real input signal Xxxx.y
*)
   #endif
                 RD        500MS                ;"500 ms"
                 WRT       m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"

!! ---------------------------------------------------------------------------
!! counter to specify the lenght of the deceleration dog for the
!! reference position return of the 6th axis
!! ---------------------------------------------------------------------------
   #if net_comment == 1
      #if ref_dir_6th_axis_{{PATH}} == 1 or ref_dir_6th_axis_{{PATH}} == 2
(*
{{PATH}}: counter to specify the lenght of the deceleration dog for
the reference position return of the 6th axis (contact: NC)
*)
      #endif
      #if ref_dir_6th_axis_{{PATH}} == 3 or ref_dir_6th_axis_{{PATH}} == 4
(*
{{PATH}}: counter to specify the lenght of the deceleration dog for
the reference position return of the 6th axis (contact: NO)
*)
      #endif
   #endif
                 RD        LOG1                 ;"logic one"
                 RD.STK.NOT LOG1                ;"logic one"
   #if ref_dir_6th_axis_{{PATH}} == 1 or ref_dir_6th_axis_{{PATH}} == 2
                 RD.STK    m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
   #endif
   #if ref_dir_6th_axis_{{PATH}} == 3 or ref_dir_6th_axis_{{PATH}} == 4
                 RD.STK.NOT m_iref6_{{PATH}}     ;"mem ref. switch 6th axis"
   #endif
                 RD.STK    CLOCK                ;"cycle clock pulse"
                 AND       MV6_{{PATH}}          ;"6th axis moving signal"
                 CTRC
                           psdog6_{{PATH}}       ;"preset soft. dog 6th axis"
                           csdog6_{{PATH}}       ;"counter soft. dog 6th axis"
                 WRT       inctr6_{{PATH}}       ;"increment counter 6th axis"

                 RD        inctr6_{{PATH}}       ;"increment counter 6th axis"
                 RD.STK    esdog6_{{PATH}}       ;"end soft dec. dog 6th axis"
   #if ref_dir_6th_axis_{{PATH}} == 1 or ref_dir_6th_axis_{{PATH}} == 2 
                 AND.NOT   m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
   #endif
   #if ref_dir_6th_axis_{{PATH}} == 3 or ref_dir_6th_axis_{{PATH}} == 4
                 AND       m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
   #endif
                 OR.STK
                 WRT       esdog6_{{PATH}}       ;"end soft dec. dog 6th axis"

!! ---------------------------------------------------------------------------
!! 6th axis reference position return
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: 6th axis reference position return
*)
   #endif
                 RD        M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 OR        M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND       M_K6AX_{{PATH}}       ;"memory 6th axis selected"
                 AND.NOT   AUTREF_{{PATH}}       ;"automatic reference return"
                 RD.STK    A_REF6_{{PATH}}       ;"auto ref. return 6th axis"
                 OR        R_REF6_{{PATH}}       ;"reference return 6th axis"
                 AND.NOT   ZP6_{{PATH}}          ;"ref position 6th axis"
                 OR.STK
                 AND.NOT   REFSTP_{{PATH}}       ;"stop reference return"
                 AND.NOT   NOREF6_{{PATH}}       ;"no reference return 6th axis"
                 WRT       R_REF6_{{PATH}}       ;"reference return 6th axis"

!! ---------------------------------------------------------------------------
!! 6th axis reference position return in plus direction
!! parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
!! 3003 bit 5 = 0 (DEC), 3006 bit 0 = 1 (GDC)
!! ---------------------------------------------------------------------------
   #if ref_dir_6th_axis_{{PATH}} == 1 or ref_dir_6th_axis_{{PATH}} == 3 
      #if net_comment == 1
         #if ref_dir_6th_axis_{{PATH}} == 1
(*
{{PATH}}: 6th axis reference position return in plus direction
(contact: NC, dog at negative axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
         #if ref_dir_6th_axis_{{PATH}} == 3
(*
{{PATH}}: 6th axis reference position return in plus direction
(contact: NO, dog at negative axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 0 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
      #endif
                 RD        R_REF6_{{PATH}}       ;"reference return 6th axis"
      #if ref_dir_6th_axis_{{PATH}} == 1
                  AND.NOT  m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
      #endif
      #if ref_dir_6th_axis_{{PATH}} == 3
                 AND       m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
      #endif
                 AND.NOT   aref_6m_{{PATH}}      ;"ref: move 6th axis -dir."
                 WRT       aref_6p_{{PATH}}      ;"ref: move 6th axis +dir."

                 RD        R_REF6_{{PATH}}       ;"reference return 6th axis"
                 AND.NOT   aref_6p_{{PATH}}      ;"ref: move 6th axis +dir."
                 WRT       aref_6m_{{PATH}}      ;"ref: move 6th axis -dir."
      #if ref_dir_6th_axis_{{PATH}} == 1
                 RD        m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
      #endif
      #if ref_dir_6th_axis_{{PATH}} == 3
                 RD.NOT    m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
      #endif
                 RD.STK    esdog6_{{PATH}}       ;"end soft dec. dog 6th axis"
                 AND       MVD6_{{PATH}}         ;"6th axis moving dir. signal"
                 OR.STK
                 OR        aref_6p_{{PATH}}      ;"ref: move 6th axis +dir."
                 WRT       *DEC6_{{PATH}}        ;"deceleration signal 6th axis"
   #endif

!! ---------------------------------------------------------------------------
!! 6th axis reference position return in minus direction
!! parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
!! 3003 bit 5 = 0 (DEC), 3006 bit 0 = 1 (GDC)
!! ---------------------------------------------------------------------------
   #if ref_dir_6th_axis_{{PATH}} == 2 or ref_dir_6th_axis_{{PATH}} == 4
      #if net_comment == 1
         #if ref_dir_6th_axis_{{PATH}} == 2
(*
{{PATH}}: 6th axis reference position return in minus direction
(contact: NC, dog at positive axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
         #if ref_dir_6th_axis_{{PATH}} == 4
(*
{{PATH}}: 6th axis reference position return in minus direction
(contact: NO, dog at positive axis limit)
parameter setting: 1005 bit 1 = 0 (DLZx), 1006 bit 5 = 1 (ZMI),
3003 bit 5 = 0 (DEC),  3006 bit 0 = 1 (GDC)
*)
         #endif
      #endif
                 RD        R_REF6_{{PATH}}       ;"reference return 6th axis"
      #if ref_dir_6th_axis_{{PATH}} == 2
                 AND.NOT   m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
      #endif
      #if ref_dir_6th_axis_{{PATH}} == 4
                 AND       m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
      #endif
                 AND.NOT   aref_6p_{{PATH}}      ;"ref: move 6th axis +dir."
                 WRT       aref_6m_{{PATH}}      ;"ref: move 6th axis -dir."

                 RD        R_REF6_{{PATH}}       ;"reference return 6th axis"
                 AND.NOT   aref_6m_{{PATH}}      ;"ref: move 6th axis -dir."
                 WRT       aref_6p_{{PATH}}      ;"ref: move 6th axis +dir."
      #if ref_dir_6th_axis_{{PATH}} == 2
                 RD        m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
      #endif
      #if ref_dir_6th_axis_{{PATH}} == 4
                 RD.NOT    m_iref6_{{PATH}}      ;"mem ref. switch 6th axis"
      #endif
                 RD.STK    esdog6_{{PATH}}       ;"end soft dec. dog 6th axis"
                 AND.NOT   MVD6_{{PATH}}         ;"6th axis moving dir. signal"
                 OR.STK
                 OR        aref_6m_{{PATH}}      ;"ref: move 6th axis -dir."
                 WRT       *DEC6_{{PATH}}        ;"deceleration signal 6th axis"
   #endif

#endif
