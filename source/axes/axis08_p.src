//:--------------------------------------------------------------------------:
//: File              : axis08__PATH__.src                                       :
//: Creation date     : 16.09.2013                                           :
//: Last modification : see table below                                      :
//: Author            : FANUC FA Switzerland / Peter Bloesch                 :
//: Description       : Management of 8th axis                               :
//:--------------------------------------------------------------------------:
//
//:--------------------------------------------------------------------------:
//:                     Modification                                         :
//:--------------------------------------------------------------------------:
//: Date   :Version: Sign  :Description                                      :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:

// ===========================================================================
// Code
// ===========================================================================

// ---------------------------------------------------------------------------
// _PATH__: overtravel check
// if there are limit switches for the 8th axis,
// replace LOG1 with the real input signals Xxxx.y
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: overtravel check
if there are limit switches for the 8th axis,
replace LOG1 with the real input signals Xxxx.y
*)
#endif
                 RD        LOG1                 ;"logic one"
                 WRT       *+L8__PATH__         ;"overtravel + 8th axis"

                 RD        LOG1                 ;"logic one"
                 WRT       *-L8__PATH__         ;"overtravel - 8th axis"

// ---------------------------------------------------------------------------
// reference position return
// ---------------------------------------------------------------------------
#if ref_dir_8th_axis__PATH__ > 0 && ref_dir_1st_axis__PATH__ > -1
   #include "axis08ref__PATH__.src"       // reference position return 8th axis
#endif

// ---------------------------------------------------------------------------
// _PATH__: led 8th axis selected
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: led 8th axis selected
*)
#endif
                 RD        M_K8AX__PATH__       ;"memory 8th axis selected"
#if ref_dir_1st_axis__PATH__ > -1
                 AND.NOT   REFRUN__PATH__       ;"reference return active"
   #if ref_dir_8th_axis__PATH__ > 0
                 OR        R_REF8__PATH__       ;"reference return 8th axis"
   #endif
#endif
                 AND       M_MAN__PATH__        ;"manual mode active"
                 WRT       L_8AX__PATH__        ;"led 8th axis sel."

// ---------------------------------------------------------------------------
// _PATH__: jog feed
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: jog feed
*)
#endif
                 RD        M_K8AX__PATH__       ;"memory 8th axis selected"
                 AND       M_KPLUS__PATH__      ;"memory axis plus dir."
                 AND.NOT   M_REF__PATH__        ;"mode ref selected"
#if ref_dir_8th_axis__PATH__ > 0 && ref_dir_1st_axis__PATH__ > -1
                 OR        aref_8p__PATH__      ;"ref: move 8th axis +dir."
#endif
                 AND       AX_MEN__PATH__       ;"axis move enable"
#if spindle_support__PATH__ == 4 && spindle_sv_axis__PATH__ == 8 && spindle_support_P01 > -1
                 AND.NOT   EAX8__PATH__         ;"8th axis is pmc axis"
#endif
                 WRT       J8+__PATH__          ;"feed 8th axis + direction"
                 WRT       L_8AXP__PATH__       ;"led 8th axis + dir."

                 RD        M_K8AX__PATH__       ;"memory 8th axis selected"
                 AND       M_KMINUS__PATH__     ;"memory axis minus dir."
                 AND.NOT   M_REF__PATH__        ;"mode ref selected"
#if ref_dir_8th_axis__PATH__ > 0 && ref_dir_1st_axis__PATH__ > -1
                 OR        aref_8m__PATH__      ;"ref: move 8th axis -dir."
#endif
                 AND       AX_MEN__PATH__       ;"axis move enable"
#if spindle_support__PATH__ == 4 && spindle_sv_axis__PATH__ == 8 && spindle_support_P01 > -1
                 AND.NOT   EAX8__PATH__         ;"8th axis is pmc axis"
#endif
                 WRT       J8-__PATH__          ;"feed 8th axis - direction"
                 WRT       L_8AXM__PATH__       ;"led 8th axis - dir."

// ---------------------------------------------------------------------------
// _PATH__: workpiece coordinate system preset
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: workpiece coordinate system preset
*)
#endif
                 RD        REQWP8__PATH__       ;"req wp coord preset 8th axis"
                 AND       WPRST8__PATH__       ;"wp coord. preset 8th axis"
                 AND       WPSF8__PATH__        ;"wp coord. preset fin 8th axis"
                 OR        RST__PATH__          ;"resetting signal"
                 RST       REQWP8__PATH__       ;"req wp coord preset 8th axis"

                 RD        M108__PATH__         ;"wp coord preset 8th axis"
                 AND       WPRST8__PATH__       ;"wp coord preset 8th axis"
                 AND       WPSF8__PATH__        ;"wp coord preset fin 8th ax"
                 RST       M108__PATH__         ;"wp coord preset 8th axis"

                 RD        FE_RST__PATH__       ;"falling edge reset"
                 AND       wppsreq8__PATH__     ;"req wp coord. preset 8th ax"
                 RD.STK    FEPGMT__PATH__       ;"falling edge program test"
                 AND.NOT   PGMRUN__PATH__       ;"program running"
                 OR.STK    
                 OR        REQWP8__PATH__       ;"req wp coord preset 8th axis"
                 OR        M108__PATH__         ;"wp coord preset 8th axis"
                 RD.STK    WPRST8__PATH__       ;"wp coord. preset 8th axis"
                 AND.NOT   WPSF8__PATH__        ;"wp coord. preset fin 8th axis"
                 OR.STK    
                 AND.NOT   RST__PATH__          ;"resetting signal"
                 WRT       WPRST8__PATH__       ;"wp coord. preset 8th axis"

                 RD        FEPGMT__PATH__       ;"falling edge program test"
                 AND       PGMRUN__PATH__       ;"program running"
                 RD.STK    wppsreq8__PATH__     ;"req wp coord. preset 8th ax"
                 AND.NOT   FE_RST__PATH__       ;"falling edge reset"
                 OR.STK    
                 WRT       wppsreq8__PATH__     ;"req wp coord. preset 8th ax"

#if brake_8th_axis__PATH__ == 0
// ---------------------------------------------------------------------------
// _PATH__: axis interlock
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: axis interlock
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       POW_OK__PATH__       ;"power supply axis ok"
   #else
                 RD        POW_OK__PATH__       ;"power suply axis ok"
   #endif
                 AND.NOT   WPRST8__PATH__       ;"wp coord. preset 8th axis"
                 WRT       *IT8__PATH__         ;"interlock 8th axis"

// ---------------------------------------------------------------------------
// _PATH__: servo off
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: servo off
*)
   #endif
                 RD.NOT    POW_OK__PATH__       ;"power suply axis ok"
                 WRT       SVF8__PATH__         ;"servo off 8th axis"
#else
// ---------------------------------------------------------------------------
// _PATH__: brake control
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: brake control
*)
   #endif
                 RD        POW_OK__PATH__       ;"power supply axis ok"
                 AND       SA8__PATH__          ;"8th axis servo ready"
                 WRT       relb_8th__PATH__     ;"release brake 8th axis"

// ---------------------------------------------------------------------------
// _PATH__: delay brake released
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: delay brake released
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       relb_8th__PATH__     ;"release brake 8th axis"
   #else
                 RD        relb_8th__PATH__     ;"release brake 8th axis"
   #endif
                 AND.NOT   WPRST8__PATH__       ;"wp coord preset 8th axis"
                 TMRB      
                           0
                           96
                 WRT       *IT8__PATH__         ;"interlock 8th axis"

// ---------------------------------------------------------------------------
// _PATH__: delay brake clamped
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: delay brake clamped
*)
   #endif
                 RD.NOT    relb_8th__PATH__     ;"release brake 8th axis"
                 TMRB      
                           0
                           48
                 WRT       8th_soff__PATH__     ;"8th axis servo off"

// ---------------------------------------------------------------------------
// _PATH__: servo off
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: servo off
*)
   #endif
                 RD.NOT    FICYCL               ;"first cycle finished"
                 OR        1st_svf8__PATH__     ;"1st servo off 8th axis"
                 AND.NOT   POW_OK__PATH__       ;"power supply axis ok"
                 WRT       1st_svf8__PATH__     ;"1st servo off 8th axis"

                 RD        1st_svf8__PATH__     ;"1st servo off 8th axis"
                 OR        8th_soff__PATH__     ;"8th axis servo off"
                 WRT       SVF8__PATH__         ;"servo off 8th axis"
#endif
// ---------------------------------------------------------------------------
// _PATH__: external deceleration
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: external deceleration
*)
#endif
                 RD.NOT    SETUP__PATH__        ;"set-up active"
                 WRT       *+ED8__PATH__        ;"external dec. + 8th axis"
                 WRT       *-ED8__PATH__        ;"external dec. - 8th axis"

#if dcs_support == 1
// ---------------------------------------------------------------------------
// _PATH__: DCS MCC off signal
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS MCC off signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        *MCFV8__PATH__       ;"MCC off signal 8th axis"
   #endif
                 WRT       M*MCFV8__PATH__      ;"8th axis MCC off signal"

// ---------------------------------------------------------------------------
// _PATH__: DCS monitoring result signal
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS monitoring result signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        RSV8__PATH__         ;"monitoring result 8th axis"
   #endif
                 WRT       MRSV8__PATH__        ;"8th axis monit. result"

// ---------------------------------------------------------------------------
// _PATH__: position information effect signal
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
DCS position information effect signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        POSE8__PATH__        ;"pos. inf. effect 8th axis"
   #endif
                 WRT       MPOSE8__PATH__       ;"8th axis pos. inf. effect"

// ---------------------------------------------------------------------------
// _PATH__: DCS safety check activation
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS safety check activation
*)
   #endif
                 RD        *SGOPN               ;"guard state doors closed"
                 WRT       *VLDV8__PATH__       ;"safety check req. 8th ax."

// ---------------------------------------------------------------------------
// _PATH__: DCS safety zero speed monitoring
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS safety zero speed monitoring
*)
   #endif
                 RD.NOT    DCS_S02              ;"DCS speed set 2"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 WRT       SVZR8__PATH__        ;"zero mon. req. 8th axis"

// ---------------------------------------------------------------------------
// _PATH__: DCS selection of safety speed set 2
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: DCS selection of safety speed set 2
*)
   #endif
                 RD        DCS_S02              ;"DCS speed set 2"
                 WRT       SVA8__PATH__         ;"saf. sel. sig. A 8th axis"
#endif

#if brake_8th_axis__PATH__ == 1
// ---------------------------------------------------------------------------
// _PATH__: management of the brake output
// replace T_BIT000 with the real brake output Yxxx.y
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
_PATH__: management of the brake output
replace T_BIT000 with the real brake output Yxxx.y
*)
   #endif

   #if dcs_support == 0
                 RD        relb_8th__PATH__     ;"release brake 8th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #else
                 RD        relb_8th__PATH__     ;"release brake 8th axis"
                 AND       *BRK8__PATH__        ;"Safety Brake sig. 8th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #endif
#endif
