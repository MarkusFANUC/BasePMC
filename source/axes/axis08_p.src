!!:--------------------------------------------------------------------------:
!!: File              : axis08_{{PATH}}.src                                       :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Management of 8th axis                               :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:05.11.18: 2.1.0 :Blp    :In case of brake control, the activation of the  :
!!:        :       :       :*IT signal is accelerated when wp coordinate     :
!!:        :       :       :preset is executed or hold to run button is      :
!!:        :       :       :pressed.                                         :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

!! ---------------------------------------------------------------------------
!! {{PATH}}: overtravel check
!! if there are limit switches for the 8th axis,
!! replace LOG1 with the real input signals Xxxx.y
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: overtravel check
if there are limit switches for the 8th axis,
replace LOG1 with the real input signals Xxxx.y
*)
#endif
                 RD        LOG1                 ;"logic one"
                 WRT       *+L8_{{PATH}}         ;"overtravel + 8th axis"

                 RD        LOG1                 ;"logic one"
                 WRT       *-L8_{{PATH}}         ;"overtravel - 8th axis"

!! ---------------------------------------------------------------------------
!! reference position return
!! ---------------------------------------------------------------------------
#if ref_dir_8th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
   #include "axis08ref_{{PATH}}.src"       !! reference position return 8th axis
#endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: led 8th axis selected
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: led 8th axis selected
*)
#endif
                 RD        M_K8AX_{{PATH}}       ;"memory 8th axis selected"
#if ref_dir_1st_axis_{{PATH}} > -1
                 AND.NOT   REFRUN_{{PATH}}       ;"reference return active"
   #if ref_dir_8th_axis_{{PATH}} > 0
                 OR        R_REF8_{{PATH}}       ;"reference return 8th axis"
   #endif
#endif
                 AND       M_MAN_{{PATH}}        ;"manual mode active"
                 WRT       L_8AX_{{PATH}}        ;"led 8th axis sel."

!! ---------------------------------------------------------------------------
!! {{PATH}}: jog feed
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: jog feed
*)
#endif
                 RD        M_K8AX_{{PATH}}       ;"memory 8th axis selected"
                 AND       M_KPLUS_{{PATH}}      ;"memory axis plus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_8th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_8p_{{PATH}}      ;"ref: move 8th axis +dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 8 and spindle_support_P01 > -1
                 AND.NOT   EAX8_{{PATH}}         ;"8th axis is pmc axis"
#endif
                 WRT       J8+_{{PATH}}          ;"feed 8th axis + direction"
                 WRT       L_8AXP_{{PATH}}       ;"led 8th axis + dir."

                 RD        M_K8AX_{{PATH}}       ;"memory 8th axis selected"
                 AND       M_KMINUS_{{PATH}}     ;"memory axis minus dir."
                 AND.NOT   M_REF_{{PATH}}        ;"mode ref selected"
#if ref_dir_8th_axis_{{PATH}} > 0 and ref_dir_1st_axis_{{PATH}} > -1
                 OR        aref_8m_{{PATH}}      ;"ref: move 8th axis -dir."
#endif
                 AND       AX_MEN_{{PATH}}       ;"axis move enable"
#if spindle_support_{{PATH}} == 4 and spindle_sv_axis_{{PATH}} == 8 and spindle_support_P01 > -1
                 AND.NOT   EAX8_{{PATH}}         ;"8th axis is pmc axis"
#endif
                 WRT       J8-_{{PATH}}          ;"feed 8th axis - direction"
                 WRT       L_8AXM_{{PATH}}       ;"led 8th axis - dir."

!! ---------------------------------------------------------------------------
!! {{PATH}}: workpiece coordinate system preset
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: workpiece coordinate system preset
*)
#endif
                 RD        REQWP8_{{PATH}}       ;"req wp coord preset 8th axis"
                 AND       WPRST8_{{PATH}}       ;"wp coord. preset 8th axis"
                 AND       WPSF8_{{PATH}}        ;"wp coord. preset fin 8th axis"
                 OR        RST_{{PATH}}          ;"resetting signal"
                 RST       REQWP8_{{PATH}}       ;"req wp coord preset 8th axis"

                 RD        M108_{{PATH}}         ;"wp coord preset 8th axis"
                 AND       WPRST8_{{PATH}}       ;"wp coord preset 8th axis"
                 AND       WPSF8_{{PATH}}        ;"wp coord preset fin 8th ax"
                 RST       M108_{{PATH}}         ;"wp coord preset 8th axis"

                 RD        FE_RST_{{PATH}}       ;"falling edge reset"
                 AND       wppsreq8_{{PATH}}     ;"req wp coord. preset 8th ax"
                 RD.STK    FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND.NOT   PGMRUN_{{PATH}}       ;"program running"
                 OR.STK    
                 OR        REQWP8_{{PATH}}       ;"req wp coord preset 8th axis"
                 OR        M108_{{PATH}}         ;"wp coord preset 8th axis"
                 RD.STK    WPRST8_{{PATH}}       ;"wp coord. preset 8th axis"
                 AND.NOT   WPSF8_{{PATH}}        ;"wp coord. preset fin 8th axis"
                 OR.STK    
                 AND.NOT   RST_{{PATH}}          ;"resetting signal"
                 WRT       WPRST8_{{PATH}}       ;"wp coord. preset 8th axis"

                 RD        FEPGMT_{{PATH}}       ;"falling edge program test"
                 AND       PGMRUN_{{PATH}}       ;"program running"
                 RD.STK    wppsreq8_{{PATH}}     ;"req wp coord. preset 8th ax"
                 AND.NOT   FE_RST_{{PATH}}       ;"falling edge reset"
                 OR.STK    
                 WRT       wppsreq8_{{PATH}}     ;"req wp coord. preset 8th ax"

#if brake_8th_axis_{{PATH}} == 0
!! ---------------------------------------------------------------------------
!! {{PATH}}: axis interlock
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: axis interlock
*)
   #endif
   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       POW_OK_{{PATH}}       ;"power supply axis ok"
   #else
                 RD        POW_OK_{{PATH}}       ;"power suply axis ok"
   #endif
                 AND.NOT   WPRST8_{{PATH}}       ;"wp coord. preset 8th axis"
                 WRT       *IT8_{{PATH}}         ;"interlock 8th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    POW_OK_{{PATH}}       ;"power suply axis ok"
                 WRT       SVF8_{{PATH}}         ;"servo off 8th axis"
#else
!! ---------------------------------------------------------------------------
!! {{PATH}}: brake control
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: brake control
*)
   #endif
                 RD        POW_OK_{{PATH}}       ;"power supply axis ok"
                 AND       SA8_{{PATH}}          ;"8th axis servo ready"
                 WRT       relb_8th_{{PATH}}     ;"release brake 8th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake released
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake released
*)
   #endif
                 RD        relb_8th_{{PATH}}     ;"release brake 8th axis"
                 TMRB      
                           0
                           96
                 WRT       brel_8th_{{PATH}}     ;"brake 8th axis released"

   #if dcs_support == 1
                 RD        *SGOPN               ;"guard state doors closed"
                 OR        SET_M01_EN           ;"enable setup mode 01"
                 AND       brel_8th_{{PATH}}     ;"brake 8th axis released"
   #else
                 RD        brel_8th_{{PATH}}     ;"brake 8th axis released"
   #endif
                 AND.NOT   WPRST8_{{PATH}}       ;"wp coord preset 8th axis"
                 WRT       *IT8_{{PATH}}         ;"interlock 8th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay brake clamped
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: delay brake clamped
*)
   #endif
                 RD.NOT    relb_8th_{{PATH}}     ;"release brake 8th axis"
                 TMRB      
                           0
                           48
                 WRT       8th_soff_{{PATH}}     ;"8th axis servo off"

!! ---------------------------------------------------------------------------
!! {{PATH}}: servo off
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: servo off
*)
   #endif
                 RD.NOT    FICYCL               ;"first cycle finished"
                 OR        1st_svf8_{{PATH}}     ;"1st servo off 8th axis"
                 AND.NOT   POW_OK_{{PATH}}       ;"power supply axis ok"
                 WRT       1st_svf8_{{PATH}}     ;"1st servo off 8th axis"

                 RD        1st_svf8_{{PATH}}     ;"1st servo off 8th axis"
                 OR        8th_soff_{{PATH}}     ;"8th axis servo off"
                 WRT       SVF8_{{PATH}}         ;"servo off 8th axis"
#endif
!! ---------------------------------------------------------------------------
!! {{PATH}}: external deceleration
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: external deceleration
*)
#endif
                 RD.NOT    SETUP_{{PATH}}        ;"set-up active"
                 WRT       *+ED8_{{PATH}}        ;"external dec. + 8th axis"
                 WRT       *-ED8_{{PATH}}        ;"external dec. - 8th axis"

#if dcs_support == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS MCC off signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS MCC off signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        *MCFV8_{{PATH}}       ;"MCC off signal 8th axis"
   #endif
                 WRT       M*MCFV8_{{PATH}}      ;"8th axis MCC off signal"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS monitoring result signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS monitoring result signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        RSV8_{{PATH}}         ;"monitoring result 8th axis"
   #endif
                 WRT       MRSV8_{{PATH}}        ;"8th axis monit. result"

!! ---------------------------------------------------------------------------
!! {{PATH}}: position information effect signal
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
DCS position information effect signal
*)
   #endif
   #if demo_control == 1
                 RD        LOG1                 ;"logic one"
   #else
                 RD        POSE8_{{PATH}}        ;"pos. inf. effect 8th axis"
   #endif
                 WRT       MPOSE8_{{PATH}}       ;"8th axis pos. inf. effect"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety check activation
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety check activation
*)
   #endif
                 RD        *SGOPN               ;"guard state doors closed"
                 WRT       *VLDV8_{{PATH}}       ;"safety check req. 8th ax."

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS safety zero speed monitoring
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS safety zero speed monitoring
*)
   #endif
                 RD.NOT    DCS_S02              ;"DCS speed set 2"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 WRT       SVZR8_{{PATH}}        ;"zero mon. req. 8th axis"

!! ---------------------------------------------------------------------------
!! {{PATH}}: DCS selection of safety speed set 2
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: DCS selection of safety speed set 2
*)
   #endif
                 RD        DCS_S02              ;"DCS speed set 2"
                 WRT       SVA8_{{PATH}}         ;"saf. sel. sig. A 8th axis"
#endif

#if brake_8th_axis_{{PATH}} == 1
!! ---------------------------------------------------------------------------
!! {{PATH}}: management of the brake output
!! replace T_BIT000 with the real brake output Yxxx.y
!! ---------------------------------------------------------------------------
   #if net_comment == 1
(*
{{PATH}}: management of the brake output
replace T_BIT000 with the real brake output Yxxx.y
*)
   #endif

   #if dcs_support == 0
                 RD        relb_8th_{{PATH}}     ;"release brake 8th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #else
                 RD        relb_8th_{{PATH}}     ;"release brake 8th axis"
                 AND       *BRK8_{{PATH}}        ;"Safety Brake sig. 8th axis"
                 WRT       T_BIT000             ;"temporary bit 000"
   #endif
#endif
