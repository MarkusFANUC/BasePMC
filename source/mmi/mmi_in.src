//:--------------------------------------------------------------------------:
//: File              : mmi_in.src                                           :
//: Creation date     : 29.11.2013                                           :
//: Last modification : see table below                                      :
//: Author            : FANUC FA Switzerland / Peter Bloesch                 :
//: Description       : Inputs man-machine-interface                         :
//:--------------------------------------------------------------------------:
//
//:--------------------------------------------------------------------------:
//:                     Modification                                         :
//:--------------------------------------------------------------------------:
//: Date   :Version: Sign  :Description                                      :
//:--------------------------------------------------------------------------:
//:05.01.15: 1.1.0 :Blp    :switching of iPendant and SOP modified           :
//:--------------------------------------------------------------------------:
//:27.07.19: 2.0.0 :Blp    :sop selection by screen notification             :
//:        :       :       :path 11-15 added                                 :
//:--------------------------------------------------------------------------:
//:21.12.18: 2.1.0 :Blp    :Portable manual pulse generator added            :
//:--------------------------------------------------------------------------:
//:25.06.21: 3.0.0 :Blp    :0i-D and 3xi-A removed                           :
//:        :       :       :virtual operator's panel added                   :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:

// ===========================================================================
// Code
// ===========================================================================

// ---------------------------------------------------------------------------
// common key inputs SOP and Small machine operator's panel with 30 keys
// ---------------------------------------------------------------------------
#if operatorspanel < 2
   #if nbr_of_path > 1
// ---------------------------------------------------------------------------
// key path sync (shift + cursor up)
// ---------------------------------------------------------------------------
      #if net_comment == 1
(*
key path sync (shift + cursor up)
*)
      #endif
                 RD        iPathSyn             ;"button path sync."
                 WRT       K_PSYN               ;"key path sync"

// ---------------------------------------------------------------------------
// key path+ (cursor up)
// ---------------------------------------------------------------------------
      #if net_comment == 1
(*
key path+ (cursor up)
*)
      #endif
                 RD        iPathPlus            ;"button path plus"
                 WRT       K_PATH+              ;"key path plus"

// ---------------------------------------------------------------------------
// key path-
// ---------------------------------------------------------------------------
      #if nbr_of_path > 2
         #if net_comment == 1
(*
key path- (cursor down)
*)
         #endif
                 RD        iPathMinus           ;"button path minus"
                 WRT       K_PATH-              ;"key path minus"
      #endif
   #endif

   #if dcs_support == 1
// ---------------------------------------------------------------------------
// protection door
// ---------------------------------------------------------------------------
      #if net_comment == 1
(*
protection door
*)
      #endif
                 RD        iDoor                ;"button prot. door"
                 WRT       K_DOOR               ;"key unlock prot. door"
   #endif
#endif

// ---------------------------------------------------------------------------
// common key inputs standard operator's panel
// ---------------------------------------------------------------------------
#if operatorspanel == 2
   #if nbr_of_path > 1
// ---------------------------------------------------------------------------
// key path sync
// ---------------------------------------------------------------------------
      #if net_comment == 1
(*
key path sync
*)
      #endif
                 RD        iF_K_D01             ;"op55: key D01"
      #if virtual_op_panel == 1 && start_address_op > 0
                 AND.NOT   VIRTUAL_OP           ;"Virtual operator panel 55Key"
                 RD.STK    iV_K_D01             ;"op55: virtual key D01"
                 AND       VIRTUAL_OP           ;"Virtual operator panel 55Key"
                 OR.STK    
      #endif
                 WRT       K_PSYN               ;"key path sync"

// ---------------------------------------------------------------------------
// key path+
// ---------------------------------------------------------------------------
      #if net_comment == 1
(*
key path+
*)
      #endif
                 RD        iF_K_D02             ;"op55: key D02"
      #if virtual_op_panel == 1 && start_address_op > 0
                 AND.NOT   VIRTUAL_OP           ;"Virtual operator panel 55Key"
                 RD.STK    iV_K_D02             ;"op55: virtual key D02"
                 AND       VIRTUAL_OP           ;"Virtual operator panel 55Key"
                 OR.STK    
      #endif
                 WRT       K_PATH+              ;"key path plus"

// ---------------------------------------------------------------------------
// key path-
// ---------------------------------------------------------------------------
      #if nbr_of_path > 2
         #if net_comment == 1
(*
key path-
*)
         #endif
                 RD        iF_K_D03             ;"op55: key D03"
         #if virtual_op_panel == 1 && start_address_op > 0
                 AND.NOT   VIRTUAL_OP           ;"Virtual operator panel 55Key"
                 RD.STK    iV_K_D03             ;"op55: virtual key D03"
                 AND       VIRTUAL_OP           ;"Virtual operator panel 55Key"
                 OR.STK    
         #endif
                 WRT       K_PATH-              ;"key path minus"
      #endif
   #endif

   #if dcs_support == 1
// ---------------------------------------------------------------------------
// protection door
// ---------------------------------------------------------------------------
      #if net_comment == 1
(*
protection door
*)
      #endif
                 RD        iF_K_E04             ;"op55: key E04"
         #if virtual_op_panel == 1 && start_address_op > 0
                 AND.NOT   VIRTUAL_OP           ;"Virtual operator panel 55Key"
                 RD.STK    iV_K_E04             ;"op55: virtual key E04"
                 AND       VIRTUAL_OP           ;"Virtual operator panel 55Key"
                 OR.STK    
         #endif
                 WRT       K_DOOR               ;"key unlock prot. door"
   #endif
#endif

// ---------------------------------------------------------------------------
// common key inputs iPendant
// ---------------------------------------------------------------------------
#if operatorspanel == 3
   #if dcs_support == 1
// ---------------------------------------------------------------------------
// protection door
// ---------------------------------------------------------------------------
      #if net_comment == 1
(*
protection door
*)
      #endif
                 RD        iDoor                ;"button prot. door"
                 WRT       K_DOOR               ;"key unlock prot. door"
   #endif
#endif

// ---------------------------------------------------------------------------
// multipath related functions
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
multipath related functions
*)
#endif

                 CALLU     MultiPath            ;"multipath related functions"

// ---------------------------------------------------------------------------
// initialization of operator panel map area
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
initialization of operator panel map area
*)
#endif
                 RD        LOG1                 ;"logic 1"
                 NUMEB
                           54
                           0
                           OPMAP                ;"op map start address"

#if handwheel_support == 2
// ---------------------------------------------------------------------------
// enable / disable hmop
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
enable / disable hmop
*)
   #endif
                 RD        MDCHEN               ;"mode change enable"
                 AND       iH_S_ON              ;"hmop: operation switch"
                 AND       FICYCL               ;"first cycle finished"
                 SET       HMOP_ACT             ;"hmop active"

                 RD        MDCHEN               ;"mode change enable"
                 AND       HMOP_ACT             ;"hmop active"
                 AND.NOT   iH_S_ON              ;"hmop: operation switch"
                 RST       HMOP_ACT             ;"hmop active"

   #if operatorspanel == 0
                 RD        HMOP_ACT             ;"hmop active"
                 RST       SOP_RUN              ;"software op-panel active"
   #endif

   #if net_comment == 1
(*
rising edge hmop active
*)
   #endif
                 RD        HMOP_ACT             ;"hmop active"
                 AND.NOT   mhmop_act            ;"memory hmop active"
                 WRT       RE_HMOP_ACT          ;"re hmop active"

                 RD        HMOP_ACT             ;"hmop active"
                 WRT       mhmop_act            ;"memory hmop active"
#endif

#if operatorspanel > 0 && soft_op_panel == 1
// ---------------------------------------------------------------------------
// enable / disable software operator panel
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
enable software operator panel
F523 = 2, F524 = 8 (802h = 2050d
*)
   #endif
                 RD        LOG1                 ;"logic one"
                 EQW       
                           2050
                           SCRNLx               ;"screen large classification"
                 WRT       sopscreen            ;"sop screen selected"

                 RD        MDCHEN               ;"mode change enable"
                 AND       sopscreen            ;"sop screen selected"
                 AND       FICYCL               ;"first cycle finished"
                 AND.NOT   mksop                ;"memory key sop"
                 WRT       reksop               ;"re key sop"

                 RD        MDCHEN               ;"mode change enable"
                 AND       sopscreen            ;"sop screen selected"
                 AND       FICYCL               ;"first cycle finished"
                 WRT       mksop                ;"memory key sop"

                 RD        reksop               ;"re key sop"
                 AND.NOT   SOP_ACT              ;"software op active"
                 RD.STK    SOP_ACT              ;"software op active"
                 AND       sopscreen            ;"sop screen selected"
                 OR.STK    
                 WRT       SOP_ACT              ;"software op active"

                 RD        SOP_RUN              ;"software op-panel active"
                 AND.NOT   SOP_ACT              ;"software op active"
   #if handwheel_support == 2
                 OR        HMOP_ACT             ;"hmop active"
   #endif
                 RST       SOP_RUN              ;"software op-panel active"

   #if net_comment == 1
(*
rising and falling edge sop active
*)
   #endif
                 RD        SOP_ACT              ;"software op active"
                 AND.NOT   msop_act             ;"memory sop active"
                 WRT       RE_SOP_ACT           ;"re sop active"

                 RD.NOT    SOP_ACT              ;"software op active"
                 AND       msop_act             ;"memory sop active"
                 WRT       FE_SOP_ACT           ;"fe sop active"

                 RD        SOP_ACT              ;"software op active"
                 WRT       msop_act             ;"memory sop active"
#endif

// ---------------------------------------------------------------------------
// operator panel key input map
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
operator panel key input map
*)
#endif

#if operatorspanel == 1                         // small operator's panel
   #if soft_op_panel == 1
                 RD.NOT    SOP_ACT              ;"software op active"
      #if handwheel_support == 2
                 AND.NOT   HMOP_ACT             ;"memory hmop acitve"
      #endif
   #else
      #if handwheel_support == 2
                 RD.NOT   HMOP_ACT              ;"memory hmop acitve"
      #else
                 RD        LOG1                 ;"logic 1"
      #endif
   #endif
                 CALL      Op_Keyb_30_In        ;"op. 30-key inputs"
#endif

#if operatorspanel == 2                         // standard operator's panel
   #if soft_op_panel == 1
                 RD.NOT    SOP_ACT              ;"software op active"
      #if handwheel_support == 2
                 AND.NOT   HMOP_ACT             ;"memory hmop acitve"
      #endif
   #else
      #if handwheel_support == 2
                 RD.NOT   HMOP_ACT              ;"memory hmop acitve"
      #else
                 RD        LOG1                 ;"logic 1"
      #endif
   #endif
                 CALL      Op_Keyb_55_In        ;"op. 55-key inputs"
   #if virtual_op_panel == 1 && start_address_op > 0
                 RDNT      iK_ESP               ;"button emergency stop"
                 RST       VIRTUAL_OP           ;"Virtual operator panel 55Key"
   #endif
#endif

#if operatorspanel == 3                         // iPendant
   #if handwheel_support == 1
// ---------------------------------------------------------------------------
// enable iPendant
// ---------------------------------------------------------------------------
      #if net_comment == 1
(*
enable iPendant
*)
      #endif
                 RD        LOG1                 ;"logic 1"
                 WRT       oP_ENBC              ;"ip: operation on/off switch"

// ---------------------------------------------------------------------------
// enable mpg of iPendant
// ---------------------------------------------------------------------------
      #if net_comment == 1
(*
enable mpg of iPendant
*)
      #endif
                 RD        M_HD_P01             ;"P01:mode HANDLE selected"
      #if nbr_of_path > 1
                 OR        M_HD_P02             ;"P02:mode HANDLE selected"
      #endif
      #if nbr_of_path > 2
                 OR        M_HD_P03             ;"P03:mode HANDLE selected"
      #endif
      #if nbr_of_path > 3
                 OR        M_HD_P04             ;"P04:mode HANDLE selected"
      #endif
      #if nbr_of_path > 4
                 OR        M_HD_P05             ;"P05:mode HANDLE selected"
      #endif
      #if nbr_of_path > 5
                 OR        M_HD_P06             ;"P06:mode HANDLE selected"
      #endif
      #if nbr_of_path > 6
                 OR        M_HD_P07             ;"P07:mode HANDLE selected"
      #endif
      #if nbr_of_path > 7
                 OR        M_HD_P08             ;"P08:mode HANDLE selected"
      #endif
      #if nbr_of_path > 8
                 OR        M_HD_P09             ;"P09:mode HANDLE selected"
      #endif
      #if nbr_of_path > 9
                 OR        M_HD_P10             ;"P10:mode HANDLE selected"
      #endif
      #if nbr_of_path > 10
                 OR        M_HD_P11             ;"P11:mode HANDLE selected"
      #endif
      #if nbr_of_path > 11
                 OR        M_HD_P12             ;"P12:mode HANDLE selected"
      #endif
      #if nbr_of_path > 12
                 OR        M_HD_P13             ;"P13:mode HANDLE selected"
      #endif
      #if nbr_of_path > 13
                 OR        M_HD_P14             ;"P14:mode HANDLE selected"
      #endif
      #if nbr_of_path > 14
                 OR        M_HD_P15             ;"P15:mode HANDLE selected"
      #endif
                 AND       oP_ENBC              ;"ip: operation on/off switch"
                 WRT       oP_MPGEN             ;"ip: mpg control signal"
   #endif

   #if soft_op_panel == 1
                 RD.NOT    SOP_ACT              ;"software op active"
      #if handwheel_support == 2
                 AND.NOT   HMOP_ACT             ;"memory hmop acitve"
      #endif
   #else
      #if handwheel_support == 2
                 RD.NOT   HMOP_ACT              ;"memory hmop acitve"
      #else
                 RD        LOG1                 ;"logic 1"
      #endif
   #endif
                 CALL      iPendant_In          ;"iPendant inputs"
#endif

#if operatorspanel == 0
   #if handwheel_support == 2
                 RD.NOT   HMOP_ACT              ;"memory hmop acitve"
   #else
                 RD        LOG1                 ;"logic 1"
   #endif
                 CALL      Op_Keyb_SOP_In       ;"software op. inputs"

                 RD        SOP_RUN              ;"software op-panel active"
                 WRT       L_SOPRUN             ;"led sop key"
#endif

#if operatorspanel > 0 && soft_op_panel == 1
                 RD        SOP_ACT              ;"software op active"
   #if handwheel_support == 2
                 AND.NOT   HMOP_ACT             ;"memory hmop acitve"
   #endif
                 CALL      Op_Keyb_SOP_In       ;"software op. inputs"

                 RD        SOP_RUN              ;"software op-panel active"
                 WRT       L_SOPRUN             ;"led sop key"
#endif


#if handwheel_support == 2
                 RD        HMOP_ACT             ;"memory hmop acitve"
                 CALL      Hmop_In              ;"hmop inputs"
#endif

#if handwheel_support == 3
// ---------------------------------------------------------------------------
// enable Portable manual pulse generator 
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
enable Portable manual pulse generator
*)
   #endif
                 RD       MDCHEN_P01           ;"P01:mode change enable"
                 AND       PATH_P01             ;"path 01 selected"
   #if nbr_of_path > 1
                 RD.STK    MDCHEN_P02           ;"P02:mode change enable"
                 AND       PATH_P02             ;"path 02 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 2
                 RD.STK    MDCHEN_P03           ;"P03:mode change enable"
                 AND       PATH_P03             ;"path 03 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 3
                 RD.STK    MDCHEN_P04           ;"P04:mode change enable"
                 AND       PATH_P04             ;"path 04 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 4
                 RD.STK    MDCHEN_P05           ;"P05:mode change enable"
                 AND       PATH_P05             ;"path 05 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 5
                 RD.STK    MDCHEN_P06           ;"P06:mode change enable"
                 AND       PATH_P06             ;"path 06 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 6
                 RD.STK    MDCHEN_P07           ;"P07:mode change enable"
                 AND       PATH_P07             ;"path 07 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 7
                 RD.STK    MDCHEN_P08           ;"P08:mode change enable"
                 AND       PATH_P08             ;"path 08 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 8
                 RD.STK    MDCHEN_P09           ;"P09:mode change enable"
                 AND       PATH_P09             ;"path 09 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 9
                 RD.STK    MDCHEN_P10           ;"P10:mode change enable"
                 AND       PATH_P10             ;"path 10 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 10
                 RD.STK    MDCHEN_P11           ;"P11:mode change enable"
                 AND       PATH_P11             ;"path 11 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 11
                 RD.STK    MDCHEN_P12           ;"P12:mode change enable"
                 AND       PATH_P12             ;"path 12 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 12
                 RD.STK    MDCHEN_P13           ;"P13:mode change enable"
                 AND       PATH_P13             ;"path 13 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 13
                 RD.STK    MDCHEN_P14           ;"P14:mode change enable"
                 AND       PATH_P14             ;"path 14 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 14
                 RD.STK    MDCHEN_P15           ;"P15:mode change enable"
                 AND       PATH_P15             ;"path 15 selected"
                 OR.STK    
   #endif
                 OR        PMPG_ACT             ;"pmpg active"
                 RD.STK    iPMPG_X1             ;"pmpg: input x1"
                 OR        iPMPG_X10            ;"pmpg: input x10"
                 OR        iPMPG_X100           ;"pmpg: input x100"
                 OR        iPMPG_X1000          ;"pmpg: input x1000"
                 AND.STK   
                 WRT       PMPG_ACT             ;"pmpg active"

// ---------------------------------------------------------------------------
// force active path to handle mode when pmpg is switched on 
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
force active path to handle mode when pmpg is switched on
*)
   #endif
   #if operatorspanel > 0
                 RD        PMPG_ACT             ;"pmpg active"
                 AND.NOT   mpmpg_act            ;"memory pmpg active"
	  #if soft_op_panel == 1
                 AND.NOT    SOP_ACT             ;"software op active"
      #endif
                 WRT       rempg_act            ;"re pmpg active"

                 RD        PMPG_ACT             ;"pmpg active"
      #if soft_op_panel == 1
                 AND.NOT    SOP_ACT             ;"software op active"
      #endif
                 WRT       mpmpg_act            ;"memory pmpg active"

                 RD        PATH_P01             ;"path 01 selected"
      #if nbr_of_path > 1
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
      #endif
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P01           ;"P01:force to mode handle"
      #if nbr_of_path > 1
                 RD        PATH_P02             ;"path 02 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P02           ;"P02:force to mode handle"
      #endif
      #if nbr_of_path > 2
                 RD        PATH_P03             ;"path 03 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P03           ;"P03:force to mode handle"
      #endif
      #if nbr_of_path > 3
                 RD        PATH_P04             ;"path 04 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P04           ;"P04:force to mode handle"
      #endif
      #if nbr_of_path > 4
                 RD        PATH_P05             ;"path 05 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P05           ;"P05:force to mode handle"
      #endif
      #if nbr_of_path > 5
                 RD        PATH_P06             ;"path 06 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P06           ;"P06:force to mode handle"
      #endif
      #if nbr_of_path > 6
                 RD        PATH_P07             ;"path 07 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P07           ;"P07:force to mode handle"
      #endif
      #if nbr_of_path > 7
                 RD        PATH_P08             ;"path 08 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P08           ;"P08:force to mode handle"
      #endif
      #if nbr_of_path > 8
                 RD        PATH_P09             ;"path 09 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P09           ;"P09:force to mode handle"
      #endif
      #if nbr_of_path > 9
                 RD        PATH_P10             ;"path 10 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P10           ;"P10:force to mode handle"
      #endif
      #if nbr_of_path > 10
                 RD        PATH_P11             ;"path 11 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P11           ;"P11:force to mode handle"
      #endif
      #if nbr_of_path > 11
                 RD        PATH_P12             ;"path 12 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P12           ;"P12:force to mode handle"
      #endif
      #if nbr_of_path > 12
                 RD        PATH_P13             ;"path 13 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P13           ;"P13:force to mode handle"
      #endif
      #if nbr_of_path > 13
                 RD        PATH_P14             ;"path 14 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P14           ;"P14:force to mode handle"
      #endif
      #if nbr_of_path > 14
                 RD        PATH_P15             ;"path 15 selected"
                 RD.STK    MDCHEN               ;"mode change enable"
                 AND       PATHSYN              ;"path synchronization signal"
                 OR.STK    
                 AND       rempg_act            ;"re pmpg active"
                 SET       SET_HD_P15           ;"P15:force to mode handle"
      #endif
   #endif

// ---------------------------------------------------------------------------
// conditions to call portable manual pulse coder module 
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
conditions to call portable manual pulse coder module
*)
   #endif
                 RD        M_HD_P01             ;"P01:mode HANDLE selected"
                 AND       PATH_P01             ;"path 01 selected"
   #if nbr_of_path > 1
                 RD.STK    M_HD_P02             ;"P02:mode HANDLE selected"
                 AND       PATH_P02             ;"path 02 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 2
                 RD.STK    M_HD_P03             ;"P03:mode HANDLE selected"
                 AND       PATH_P03             ;"path 03 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 3
                 RD.STK    M_HD_P04             ;"P04:mode HANDLE selected"
                 AND       PATH_P04             ;"path 04 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 4
                 RD.STK    M_HD_P05             ;"P05:mode HANDLE selected"
                 AND       PATH_P05             ;"path 05 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 5
                 RD.STK    M_HD_P06             ;"P06:mode HANDLE selected"
                 AND       PATH_P06             ;"path 06 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 6
                 RD.STK    M_HD_P07             ;"P07:mode HANDLE selected"
                 AND       PATH_P07             ;"path 07 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 7
                 RD.STK    M_HD_P08             ;"P08:mode HANDLE selected"
                 AND       PATH_P08             ;"path 08 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 8
                 RD.STK    M_HD_P09             ;"P09:mode HANDLE selected"
                 AND       PATH_P09             ;"path 09 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 9
                 RD.STK    M_HD_P10             ;"P10:mode HANDLE selected"
                 AND       PATH_P10             ;"path 10 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 10
                 RD.STK    M_HD_P11             ;"P11:mode HANDLE selected"
                 AND       PATH_P11             ;"path 11 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 11
                 RD.STK    M_HD_P12             ;"P12:mode HANDLE selected"
                 AND       PATH_P12             ;"path 12 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 12
                 RD.STK    M_HD_P13             ;"P13:mode HANDLE selected"
                 AND       PATH_P13             ;"path 13 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 13
                 RD.STK    M_HD_P14             ;"P14:mode HANDLE selected"
                 AND       PATH_P14             ;"path 14 selected"
                 OR.STK    
   #endif
   #if nbr_of_path > 14
                 RD.STK    M_HD_P15             ;"P15:mode HANDLE selected"
                 AND       PATH_P15             ;"path 15 selected"
                 OR.STK    
   #endif
                 AND       PMPG_ACT             ;"pmpg active"
                 WRT       pmpg_on              ;"portable mpg switched on"

                 RD        pmpg_on              ;"portable mpg switched on"
                 CALL      Pmpg_In              ;"pmpg inputs"

// ---------------------------------------------------------------------------
// led portable manual pulse generator active 
// ---------------------------------------------------------------------------
   #if net_comment == 1
(*
led portable manual pulse generator active
*)
   #endif
                 RD        pmpg_on              ;"portable mpg switched on"
                 WRT       oPMPG_LED            ;"pmpg: led"
#endif

// -----------------------------------------------
// path 01
// -----------------------------------------------
#include "..\..\include\mmi_in_p01.src"

// -----------------------------------------------
// path 02
// -----------------------------------------------
#if nbr_of_path > 1
   #include "..\..\include\mmi_in_p02.src"
#endif

// -----------------------------------------------
// path 03
// -----------------------------------------------
#if nbr_of_path > 2
   #include "..\..\include\mmi_in_p03.src"
#endif

// -----------------------------------------------
// path 04
// -----------------------------------------------
#if nbr_of_path > 3
   #include "..\..\include\mmi_in_p04.src"
#endif

// -----------------------------------------------
// path 05
// -----------------------------------------------
#if nbr_of_path > 4
   #include "..\..\include\mmi_in_p05.src"
#endif

// -----------------------------------------------
// path 06
// -----------------------------------------------
#if nbr_of_path > 5
   #include "..\..\include\mmi_in_p06.src"
#endif

// -----------------------------------------------
// path 07
// -----------------------------------------------
#if nbr_of_path > 6
   #include "..\..\include\mmi_in_p07.src"
#endif

// -----------------------------------------------
// path 08
// -----------------------------------------------
#if nbr_of_path > 7
   #include "..\..\include\mmi_in_p08.src"
#endif

// -----------------------------------------------
// path 09
// -----------------------------------------------
#if nbr_of_path > 8
   #include "..\..\include\mmi_in_p09.src"
#endif

// -----------------------------------------------
// path 10
// -----------------------------------------------
#if nbr_of_path > 9
   #include "..\..\include\mmi_in_p10.src"
#endif

// -----------------------------------------------
// path 11
// -----------------------------------------------
#if nbr_of_path > 10
   #include "..\..\include\mmi_in_p11.src"
#endif

// -----------------------------------------------
// path 12
// -----------------------------------------------
#if nbr_of_path > 11
   #include "..\..\include\mmi_in_p12.src"
#endif

// -----------------------------------------------
// path 13
// -----------------------------------------------
#if nbr_of_path > 12
   #include "..\..\include\mmi_in_p13.src"
#endif

// -----------------------------------------------
// path 14
// -----------------------------------------------
#if nbr_of_path > 13
   #include "..\..\include\mmi_in_p14.src"
#endif

// -----------------------------------------------
// path 15
// -----------------------------------------------
#if nbr_of_path > 14
   #include "..\..\include\mmi_in_p15.src"
#endif
