!!:--------------------------------------------------------------------------:
!!: File              : spiori1_{{PATH}}.src                                      :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Spindle orientation 1 stop position                  :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

!! ---------------------------------------------------------------------------
!! {{PATH}}: spindle orientation of the first serial spindle to a fixed 
!! position specified in parameter 4031
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: spindle orientation of the first serial spindle to a fixed 
position specified in parameter N04031
*)
#endif

!! ---------------------------------------------------------------------------
!! {{PATH}}: conditions to stop the spindle orientation
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: conditions to stop the spindle orientation
*)
#endif
                 RD        SP_CW_{{PATH}}        ;"spindle start cw"
                 OR        SP_CCW_{{PATH}}       ;"spindle start ccw"
                 AND.NOT   M019_{{PATH}}         ;"spindle orientation"
                 OR        M005_{{PATH}}         ;"spindle stop"
                 OR        M_MAN_{{PATH}}        ;"manual mode active"
                 OR        RST_{{PATH}}          ;"resetting signal"
                 OR.NOT    POW_OK_{{PATH}}       ;"power suply axis ok"
#if rigid_tapping_{{PATH}} == 1
                 OR        RTAP_{{PATH}}         ;"rigid tapping in progress"
#endif
#if cs_contour_ctrl_{{PATH}} == 1
                 OR        M027_{{PATH}}         ;"Cs contour ctrl on"
#endif
#if dcs_support == 1
                 RD.STK.NOT HTR                 ;"hold to run button"
                 OR.NOT    SET_M01              ;"setup mode 01"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 OR.STK    
#endif
                 WRT       or_stp_{{PATH}}       ;"stop spindle orientation"

!! ---------------------------------------------------------------------------
!! {{PATH}}: spindle orientation
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: spindle orientation
*)
#endif
                 RD        M019_{{PATH}}         ;"spindle orientation"
                 OR        ORCMA_{{PATH}}        ;"orientation command signal"
                 AND.NOT   or_stp_{{PATH}}       ;"stop spindle orientation"
                 WRT       ORCMA_{{PATH}}        ;"orientation command signal"

!! ---------------------------------------------------------------------------
!! delay spindle in position
!! global variable OR_FIN (orientation finished)
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: delay spindle in position
global variable OR_FIN (orientation finished)
*)
#endif
                 RD        ORARA_{{PATH}}        ;"orient. completion signal"
#if demo_control == 1
                 OR        LOG1                 ;"logic one"
#endif
                 AND       M019_{{PATH}}         ;"spindle orientation"
                 TMRB
                           0
                           200
                 WRT       OR_FIN_{{PATH}}       ;"sp orientation finished"

!! --------------------------------------------------------------------------
!! {{PATH}}: management of M19
!! --------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: management of M19
*)
#endif
                 RD        M019_{{PATH}}         ;"spindle orientation"
                 AND       OR_FIN_{{PATH}}       ;"sp orientation finished"
                 RST       M019_{{PATH}}         ;"spindle orientation"
