//:--------------------------------------------------------------------------:
//: File              : spiori1__PATH__.src                                      :
//: Creation date     : 16.09.2013                                           :
//: Last modification : see table below                                      :
//: Author            : FANUC FA Switzerland / Peter Bloesch                 :
//: Description       : Spindle orientation 1 stop position                  :
//:--------------------------------------------------------------------------:
//
//:--------------------------------------------------------------------------:
//:                     Modification                                         :
//:--------------------------------------------------------------------------:
//: Date   :Version: Sign  :Description                                      :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:
//:        :       :       :                                                 :
//:--------------------------------------------------------------------------:

// ===========================================================================
// Code
// ===========================================================================

// ---------------------------------------------------------------------------
// _PATH__: spindle orientation of the first serial spindle to a fixed 
// position specified in parameter 4031
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: spindle orientation of the first serial spindle to a fixed 
position specified in parameter N04031
*)
#endif

// ---------------------------------------------------------------------------
// _PATH__: conditions to stop the spindle orientation
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: conditions to stop the spindle orientation
*)
#endif
                 RD        SP_CW__PATH__        ;"spindle start cw"
                 OR        SP_CCW__PATH__       ;"spindle start ccw"
                 AND.NOT   M019__PATH__         ;"spindle orientation"
                 OR        M005__PATH__         ;"spindle stop"
                 OR        M_MAN__PATH__        ;"manual mode active"
                 OR        RST__PATH__          ;"resetting signal"
                 OR.NOT    POW_OK__PATH__       ;"power suply axis ok"
#if rigid_tapping__PATH__ == 1
                 OR        RTAP__PATH__         ;"rigid tapping in progress"
#endif
#if cs_contour_ctrl__PATH__ == 1
                 OR        M027__PATH__         ;"Cs contour ctrl on"
#endif
#if dcs_support == 1
                 RD.STK.NOT HTR                 ;"hold to run button"
                 OR.NOT    SET_M01              ;"setup mode 01"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 OR.STK    
#endif
                 WRT       or_stp__PATH__       ;"stop spindle orientation"

// ---------------------------------------------------------------------------
// _PATH__: spindle orientation
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: spindle orientation
*)
#endif
                 RD        M019__PATH__         ;"spindle orientation"
                 OR        ORCMA__PATH__        ;"orientation command signal"
                 AND.NOT   or_stp__PATH__       ;"stop spindle orientation"
                 WRT       ORCMA__PATH__        ;"orientation command signal"

// ---------------------------------------------------------------------------
// delay spindle in position
// global variable OR_FIN (orientation finished)
// ---------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: delay spindle in position
global variable OR_FIN (orientation finished)
*)
#endif
                 RD        ORARA__PATH__        ;"orient. completion signal"
#if demo_control == 1
                 OR        LOG1                 ;"logic one"
#endif
                 AND       M019__PATH__         ;"spindle orientation"
                 TMRB
                           0
                           200
                 WRT       OR_FIN__PATH__       ;"sp orientation finished"

// --------------------------------------------------------------------------
// _PATH__: management of M19
// --------------------------------------------------------------------------
#if net_comment == 1
(*
_PATH__: management of M19
*)
#endif
                 RD        M019__PATH__         ;"spindle orientation"
                 AND       OR_FIN__PATH__       ;"sp orientation finished"
                 RST       M019__PATH__         ;"spindle orientation"
