!!:--------------------------------------------------------------------------:
!!: File              : spiori2_{{PATH}}.src                                      :
!!: Creation date     : 16.09.2013                                           :
!!: Last modification : see table below                                      :
!!: Author            : FANUC FA Switzerland / Peter Bloesch                 :
!!: Description       : Spindle orientation with external stop position      :
!!:--------------------------------------------------------------------------:
!!
!!:--------------------------------------------------------------------------:
!!:                     Modification                                         :
!!:--------------------------------------------------------------------------:
!!: Date   :Version: Sign  :Description                                      :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:
!!:        :       :       :                                                 :
!!:--------------------------------------------------------------------------:

!! ===========================================================================
!! Code
!! ===========================================================================

!! ---------------------------------------------------------------------------
!! {{PATH}}: calculation of the stop position
!! programming M19 S1000 (S1000 -> 100.0 degree)
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: calculation of the stop position
programming M19 S1000 (S1000 -> 100.0 degree)
*)
#endif
                 RD        RST_{{PATH}}          ;"resetting signal"
                 RD.STK    M019_{{PATH}}         ;"spindle orientation"
                 MULB
                           4
                           S00-S31_{{PATH}}      ;"spindle speed code"
                           4096
                           TMP_B00              ;"temorary byte 0"
                 WRT       em_m19_{{PATH}}       ;"error multiplication m19"

                 RD        RST_{{PATH}}          ;"resetting signal"
                 RD.STK    M019_{{PATH}}         ;"spindle orientation"
                 DIVB
                           4
                           TMP_B00              ;"temorary byte 0"
                           3600
                           TMP_B00              ;"temorary byte 0"
                 WRT       ed_m19_{{PATH}}       ;"error division m19"
                 RD        M019_{{PATH}}         ;"spindle orientation"
                 MOVW
                           TMP_B00              ;"temorary byte 0"
                           SHA_{{PATH}}          ;"sha00-sha11"

!! ---------------------------------------------------------------------------
!! {{PATH}}: conditions to stop the spindle orientation
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: conditions to stop the spindle orientation
*)
#endif
                 RD        SP_CW_{{PATH}}        ;"spindle start cw"
                 OR        SP_CCW_{{PATH}}       ;"spindle start ccw"
                 AND.NOT   M019_{{PATH}}         ;"spindle orientation"
                 OR        M005_{{PATH}}         ;"spindle stop"
                 OR        M_MAN_{{PATH}}        ;"manual mode active"
                 OR        RST_{{PATH}}          ;"resetting signal"
                 OR.NOT    POW_OK_{{PATH}}       ;"power suply axis ok"
#if rigid_tapping_{{PATH}} == 1
                 OR        RTAP_{{PATH}}         ;"rigid tapping in progress"
#endif
#if cs_contour_ctrl_{{PATH}} == 1
                 OR        M027_{{PATH}}         ;"Cs contour ctrl on"
#endif
#if dcs_support == 1
                 RD.STK.NOT HTR                 ;"hold to run button"
                 OR.NOT    SET_M01              ;"setup mode 01"
                 AND.NOT   *SGOPN               ;"guard state doors closed"
                 OR.STK    
#endif
                 WRT       or_stp_{{PATH}}       ;"stop spindle orientation"

!! ---------------------------------------------------------------------------
!! {{PATH}}: spindle orientation
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: spindle orientation
*)
#endif
                 RD        M019_{{PATH}}         ;"spindle orientation"
                 AND.NOT   or_std_{{PATH}}       ;"delay start orientation"
                 AND.NOT   or_run_{{PATH}}       ;"spindle orientation active"
                 TMRB
                           0
                           50
                 WRT       or_std_{{PATH}}       ;"delay start orientation"

                 RD        M019_{{PATH}}         ;"spindle orientation"
                 AND       or_std_{{PATH}}       ;"delay start orientation"
                 AND.NOT   or_run_{{PATH}}       ;"spindle orientation active"
                 OR        ORCMA_{{PATH}}        ;"orientation command signal"
                 AND.NOT   or_stp_{{PATH}}       ;"stop spindle orientation"
                 WRT       ORCMA_{{PATH}}        ;"orientation command signal"

!! ---------------------------------------------------------------------------
!! {{PATH}}: orientation position change signal
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: orientation position change signal
*)
#endif
                 RD        M019_{{PATH}}         ;"spindle orientation"
                 AND       ORCMA_{{PATH}}        ;"orientation command signal"
                 AND.NOT   or_run_{{PATH}}       ;"spindle orientation active"
                 OR        INDXA_{{PATH}}        ;"orient stop pos. change"
                 AND.NOT   oridxf_{{PATH}}       ;"orient. pos. change fin"
                 AND.NOT   or_stp_{{PATH}}       ;"stop spindle orientation"
                 WRT       INDXA_{{PATH}}        ;"orient stop pos. change"

                 RD        INDXA_{{PATH}}        ;"orient stop pos. change"
                 TMRB
                           0
                           50
                 WRT       oridxf_{{PATH}}       ;"orient. pos. change fin"

!! ---------------------------------------------------------------------------
!! {{PATH}}: delay spindle in position
!! global variable OR_FIN_{{PATH}} (orientation finished)
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: delay spindle in position
global variable OR_FIN_{{PATH}} (orientation finished)
*)
#endif
                 RD        ORARA_{{PATH}}        ;"orient. completion signal"
#if demo_control == 1
                 OR        LOG1                 ;"logic one"
#endif
                 AND       M019_{{PATH}}         ;"spindle orientation"
                 AND       or_run_{{PATH}}       ;"spindle orientation active"
                 TMRB
                           0
                           200
                 WRT       OR_FIN_{{PATH}}       ;"sp orientation finished"

!! ---------------------------------------------------------------------------
!! {{PATH}}: orientation cycle active
!! ---------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: orientation cycle active
*)
#endif
                 RD        INDXA_{{PATH}}        ;"orient stop pos. change"
                 OR        or_run_{{PATH}}       ;"spindle orientation active"
                 AND.NOT   OR_FIN_{{PATH}}       ;"sp orientation finished"
                 AND.NOT   or_stp_{{PATH}}       ;"stop spindle orientation"
                 WRT       or_run_{{PATH}}       ;"spindle orientation active"

!! --------------------------------------------------------------------------
!! {{PATH}}: management of M19
!! --------------------------------------------------------------------------
#if net_comment == 1
(*
{{PATH}}: management of M19
*)
#endif
                 RD        M019_{{PATH}}         ;"spindle orientation"
                 AND       OR_FIN_{{PATH}}       ;"sp orientation finished"
                 RST       M019_{{PATH}}         ;"spindle orientation"
